namespace FsVoice.Retrieval

open System
open System.IO
open System.Net.Http
open System.Security.Cryptography
open System.Text.Json
open System.Text.Json.Serialization
open System.Threading
open FSharp.Control
open Microsoft.Extensions.AI
open System.Text
open System.Text.RegularExpressions
open FsVoice.Core
open FsVoice.Ctx

module KnowledgeSources =
    [<Literal>]
    let private DEFAULT_KEYWORD_METADATA_PARALLELISM = 5

    [<Literal>]
    let private DEFAULT_KEYWORD_METADATA_BATCH_TIMEOUT_MS = 90000

    type RetrievalIndex =
        { sources: KnowledgeSource list
          chunks: SourceChunk list
          colbertIndices: (KnowledgeSource * FsColbert.ColbertIndex) list
          encoder: FsColbert.OnnxColbertEncoder option }

    type IndexPreviewVectorSummary =
        { tokenCount: int
          embeddingDim: int
          valueSample: float32 list }

    type IndexPreviewRecord =
        { index: int
          text: string
          keywords: string list
          terms: string list
          vector: IndexPreviewVectorSummary }

    type IndexPreview =
        { source: KnowledgeSource
          totalChunks: int
          sampledCount: int
          records: IndexPreviewRecord list }

    let emptyIndex =
        { sources = []
          chunks = []
          colbertIndices = []
          encoder = None }

    [<JsonConverter(typeof<JsonStringEnumConverter>)>]
    [<RequireQualifiedAccess>]
    type QueryType =
        | Question = 1
        | SectionRetrieval = 2

    type Expansion =
        { terms: string list
          rewrittenQueries: string list
          sectionName: string option
          queryType: QueryType }

    type KeywordGenerationOptions =
        { enabled: bool
          client: IChatClient option
          modelId: string
          schemaVersion: string
          batchSize: int
          parallelism: int
          maxOutputTokens: int
          cancellationToken: CancellationToken option
          plugInProfile: QaPlugInProfile
          plugInFingerprint: string }

    module KeywordGenerationOptions =
        let defaults =
            { enabled = true
              client = None
              modelId = "gpt-5-nano"
              schemaVersion = "passage-keywords-v1"
              batchSize = 4
              parallelism = DEFAULT_KEYWORD_METADATA_PARALLELISM
              maxOutputTokens = 25000
              cancellationToken = None
              plugInProfile = QaPlugInProfile.generic
              plugInFingerprint = "" }

        let disabled = { defaults with enabled = false }

    [<RequireQualifiedAccess>]
    type PdfParsingMode =
        | Legacy
        | Hybrid
        | HybridWithoutLayout

    module PdfParsingModes =
        let displayName mode =
            match mode with
            | PdfParsingMode.Legacy -> "Legacy"
            | PdfParsingMode.Hybrid -> "Hybrid"
            | PdfParsingMode.HybridWithoutLayout -> "Hybrid without layout analysis"

        let fingerprint mode =
            match mode with
            | PdfParsingMode.Legacy -> "legacy"
            | PdfParsingMode.Hybrid -> "hybrid"
            | PdfParsingMode.HybridWithoutLayout -> "hybrid-no-layout"

        [<Literal>]
        let parserQualityVersion = "layout-sparse-fallback-v1"

        let indexFingerprint mode =
            [ $"pdfParsingMode={fingerprint mode}"
              $"pdfParserQuality={parserQualityVersion}" ]
            |> String.concat "\n"

    type PdfIngestionOptions =
        { parsingMode: PdfParsingMode
          visualDescriptions: PdfVisualDescriptionOptions }

    module PdfIngestionOptions =
        let create parsingMode =
            { parsingMode = parsingMode
              visualDescriptions = PdfVisualDescriptionOptions.disabled }

        let defaults = create PdfParsingMode.Hybrid

        let sanitize (options: PdfIngestionOptions) =
            { options with
                visualDescriptions = PdfVisualDescriptionOptions.sanitize options.visualDescriptions }

        let visualFingerprint options =
            options.visualDescriptions |> PdfVisualDescriptionOptions.fingerprint

    [<CLIMutable>]
    type InstalledPrebuiltIndex =
        { id: string
          kind: string
          displayName: string
          storedPath: string
          indexPath: string }

    [<CLIMutable>]
    type private PersistedIndexMetadata =
        { sourceFingerprint: string
          sourceLocation: string
          sourceDisplayName: string
          sourceKind: string
          parserFingerprint: string option
          pdfParsingMode: string option
          keywordFingerprint: string
          createdAtUtc: DateTimeOffset }

    let mutable private cachedEncoder: FsColbert.OnnxColbertEncoder option = None

    let private pdfIndexVersion = FsColbert.DocumentChunking.representationVersion

    let disposeIndex (retrieval: RetrievalIndex) =
        retrieval.encoder
        |> Option.iter (fun encoder ->
            (encoder :> IDisposable).Dispose()

            if Some encoder = cachedEncoder then
                cachedEncoder <- None)

    let private fsKameChunkOptions = FsColbert.ChunkOptions.fsKameDefaults

    type private PassageLoadMode =
        | LegacyPdf
        | HybridPdf of
            storageRoot: string *
            report: (string -> unit) *
            enableLayoutAnalysis: bool *
            visualDescriptions: PdfVisualDescriptionOptions

    [<AllowNullLiteral>]
    type JsonKnowledgeDocumentDto() =
        member val id: string = null with get, set
        member val label: string = null with get, set
        member val key: string = null with get, set
        member val title: string = null with get, set
        member val heading: string = null with get, set
        member val name: string = null with get, set
        member val text: string = null with get, set
        member val body: string = null with get, set
        member val answer: string = null with get, set
        member val content: string = null with get, set
        member val keywords: string array = null with get, set

    [<AllowNullLiteral>]
    type JsonKnowledgeEnvelopeDto() =
        member val documents: JsonKnowledgeDocumentDto array = null with get, set

    type JsonKnowledgeDocument =
        { id: string
          title: string
          text: string
          keywords: string list }

    let private jsonOptions =
        let options = JsonSerializerOptions(PropertyNameCaseInsensitive = true)
        options.PropertyNamingPolicy <- JsonNamingPolicy.CamelCase
        options.NumberHandling <- JsonNumberHandling.AllowReadingFromString
        options.Converters.Add(JsonFSharpConverter())
        options

    let private tryDeserialize<'T> (text: string) =
        try
            JsonSerializer.Deserialize<'T>(text, jsonOptions) |> Some
        with _ ->
            None

    let private normalizeJsonKnowledgeDocument (item: JsonKnowledgeDocumentDto) =
        Option.ofObj item.text
        |> Option.orElse (Option.ofObj item.body)
        |> Option.orElse (Option.ofObj item.answer)
        |> Option.orElse (Option.ofObj item.content)
        |> Option.bind Text.notEmpty
        |> Option.map (fun text ->
            { id =
                [ item.id; item.label; item.key ]
                |> List.choose Option.ofObj
                |> List.tryHead
                |> Option.defaultValue ""
              title =
                [ item.title; item.heading; item.name ]
                |> List.choose Option.ofObj
                |> List.tryHead
                |> Option.defaultValue ""
              text = text
              keywords =
                Option.ofObj item.keywords
                |> Option.map Array.toList
                |> Option.defaultValue []
                |> List.choose Text.notEmpty
                |> List.distinctBy _.ToLowerInvariant() })

    let private jsonKnowledgeDocuments (text: string) =
        tryDeserialize<JsonKnowledgeDocumentDto list> text
        |> Option.orElseWith (fun () ->
            tryDeserialize<JsonKnowledgeEnvelopeDto> text
            |> Option.bind (fun envelope -> Option.ofObj envelope.documents |> Option.map Array.toList))
        |> Option.orElseWith (fun () -> tryDeserialize<JsonKnowledgeDocumentDto> text |> Option.map List.singleton)
        |> Option.defaultValue []
        |> List.choose normalizeJsonKnowledgeDocument

    let private hasDoclingSchema (text: string) =
        try
            use document = JsonDocument.Parse text
            let root = document.RootElement

            if root.ValueKind <> JsonValueKind.Object then
                false
            else
                let mutable schemaName = Unchecked.defaultof<JsonElement>

                root.TryGetProperty("schema_name", &schemaName)
                && schemaName.ValueKind = JsonValueKind.String
                && String.Equals(schemaName.GetString(), FsColbert.DoclingJson.schemaName, StringComparison.Ordinal)
        with _ ->
            false

    let private jsonPassages (passageSource: FsColbert.PassageSource) path =
        async {
            try
                let json = File.ReadAllText path

                if hasDoclingSchema json then
                    match FsColbert.DoclingJson.tryDeserialize json with
                    | Error err ->
                        return Error $"Unable to read document structure JSON knowledge source '{path}': {err}"
                    | Ok document ->
                        let passages =
                            FsColbert.DoclingPassages.toPassages fsKameChunkOptions passageSource document

                        if List.isEmpty passages then
                            return Error $"No document structure passages were found in {path}."
                        else
                            return Ok passages
                else
                    let documents = jsonKnowledgeDocuments json

                    if List.isEmpty documents then
                        return Error $"No JSON knowledge documents were found in {path}."
                    else
                        return
                            documents
                            |> List.mapi (fun index item ->
                                let title = item.title |> Text.notEmpty |> Option.orElse (item.id |> Text.notEmpty)

                                let text =
                                    match title with
                                    | Some title -> $"{title}\n{item.text}"
                                    | None -> item.text

                                ({ sourceId = passageSource.id
                                   sourceDisplayName = passageSource.displayName
                                   sourceLocation = passageSource.location
                                   index = index
                                   text = Text.normalizeWhitespace text
                                   keywords = item.keywords }
                                : FsColbert.PassageRef))
                            |> Ok
            with ex ->
                return Error $"Unable to read JSON knowledge source '{path}': {ex.Message}"
        }

    let private throwIfCancellationRequested (cancellationToken: CancellationToken) =
        cancellationToken.ThrowIfCancellationRequested()

    let private sourcePassagesWithCancellation mode (source: KnowledgeSource) (cancellationToken: CancellationToken) =
        let passageSource =
            FsColbert.PassageSource.create source.location source.DisplayName source.location

        let checkedRead operation =
            async {
                throwIfCancellationRequested cancellationToken
                let! result = operation
                throwIfCancellationRequested cancellationToken
                return result
            }

        match source.kind with
        | Pdf ->
            match mode with
            | LegacyPdf ->
                checkedRead (FsColbert.PdfDocuments.readPassages fsKameChunkOptions passageSource source.location)
            | HybridPdf(storageRoot, report, enableLayoutAnalysis, visualDescriptions) ->
                let options =
                    { DoclingHybrid.currentDefaultOptions () with
                        enableLayoutAnalysis = enableLayoutAnalysis
                        visualDescriptions = PdfVisualDescriptionOptions.sanitize visualDescriptions }

                DoclingHybrid.readPdfPassagesWithOptionsWithFallbackAndCancellation
                    options
                    storageRoot
                    report
                    fsKameChunkOptions
                    passageSource
                    source.location
                    (fun () -> FsColbert.PdfDocuments.readPassages fsKameChunkOptions passageSource source.location)
                    cancellationToken
        | Markdown ->
            checkedRead (FsColbert.MarkdownDocuments.readPassages fsKameChunkOptions passageSource source.location)
        | Json -> checkedRead (jsonPassages passageSource source.location)

    let private sourcePassages mode source =
        sourcePassagesWithCancellation mode source CancellationToken.None

    let loadPassages source = sourcePassages LegacyPdf source

    let loadPassagesForIndexingWithOptionsWithCancellation
        storageRoot
        report
        (pdfOptions: PdfIngestionOptions)
        source
        cancellationToken
        =
        let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

        match pdfOptions.parsingMode with
        | PdfParsingMode.Legacy -> sourcePassagesWithCancellation LegacyPdf source cancellationToken
        | PdfParsingMode.Hybrid ->
            sourcePassagesWithCancellation
                (HybridPdf(storageRoot, report, true, pdfOptions.visualDescriptions))
                source
                cancellationToken
        | PdfParsingMode.HybridWithoutLayout ->
            sourcePassagesWithCancellation
                (HybridPdf(storageRoot, report, false, PdfVisualDescriptionOptions.disabled))
                source
                cancellationToken

    let loadPassagesForIndexingWithCancellation storageRoot report pdfParsingMode source cancellationToken =
        loadPassagesForIndexingWithOptionsWithCancellation
            storageRoot
            report
            (PdfIngestionOptions.create pdfParsingMode)
            source
            cancellationToken

    let loadPassagesForIndexing storageRoot report pdfParsingMode source =
        loadPassagesForIndexingWithCancellation storageRoot report pdfParsingMode source CancellationToken.None

    let loadPassagesForIndexingWithOptions storageRoot report pdfOptions source =
        loadPassagesForIndexingWithOptionsWithCancellation storageRoot report pdfOptions source CancellationToken.None

    let loadChunks (sources: KnowledgeSource list) : Async<SourceChunk list * string list> =
        async {
            let! loaded =
                sources
                |> List.filter _.enabled
                |> List.map (fun source ->
                    async {
                        let! result = sourcePassages LegacyPdf source
                        return source, result
                    })
                |> Async.Parallel

            let chunks = ResizeArray<SourceChunk>()
            let errors = ResizeArray<string>()

            for source, result in loaded do
                match result with
                | Ok passages ->
                    for passage in passages do
                        chunks.Add(
                            { source = source
                              index = passage.index
                              text = passage.text
                              score = 0.0f }
                        )
                | Error err -> errors.Add err

            return List.ofSeq chunks, List.ofSeq errors
        }

    let private sourceKindId sourceKind =
        match sourceKind with
        | Pdf -> "pdf"
        | Markdown -> "markdown"
        | Json -> "json"

    let private sourceFromLocation (sources: KnowledgeSource list) (location: string) : KnowledgeSource =
        sources
        |> List.tryFind (fun source -> String.Equals(source.location, location, StringComparison.OrdinalIgnoreCase))
        |> Option.defaultValue
            { kind = Pdf
              location = location
              enabled = true }

    let private hitToChunk sources (hit: FsColbert.SearchHit) =
        { source = sourceFromLocation sources hit.reference.sourceLocation
          index = hit.reference.index
          text = hit.reference.text
          score = hit.score }

    let private chunksFromIndex sources (index: FsColbert.ColbertIndex) =
        index.passages
        |> List.map (fun passage ->
            { source = sourceFromLocation sources passage.reference.sourceLocation
              index = passage.reference.index
              text = passage.reference.text
              score = 0.0f })

    let private rankLexically (query: string) (maxResults: int) (chunks: SourceChunk list) : SourceChunk list =
        let queryTerms = Text.terms query |> Set.ofList

        if Set.isEmpty queryTerms then
            []
        else
            chunks
            |> List.choose (fun (chunk: SourceChunk) ->
                let haystack = chunk.text.ToLowerInvariant()

                let score =
                    queryTerms |> Seq.sumBy (fun term -> if haystack.Contains term then 1 else 0)

                if score = 0 then
                    None
                else
                    Some { chunk with score = float32 score })
            |> List.sortByDescending (fun (c: SourceChunk) -> c.score, c.text.Length * -1)
            |> List.truncate maxResults

    let private sourceKey (source: KnowledgeSource) = source.location.ToLowerInvariant()

    let private chunkKey (chunk: SourceChunk) = sourceKey chunk.source, chunk.index

    let private sourceEquals (left: KnowledgeSource) (right: KnowledgeSource) =
        String.Equals(left.location, right.location, StringComparison.OrdinalIgnoreCase)

    let private distinctChunks chunks =
        chunks
        |> List.fold
            (fun (seen, kept) chunk ->
                let key = chunkKey chunk

                if Set.contains key seen then
                    seen, kept
                else
                    Set.add key seen, chunk :: kept)
            (Set.empty, [])
        |> snd
        |> List.rev

    let private sourceCoverageTerms =
        [ "both"
          "each"
          "all"
          "documents"
          "document"
          "docs"
          "papers"
          "sources"
          "selected"
          "compare"
          "contrast"
          "summarize"
          "summarise"
          "summary"
          "them"
          "these" ]

    let private representativeTerms =
        [ "this"
          "it"
          "overview"
          "about"
          "gist"
          "abstract"
          "introduction"
          "main"
          "points" ]

    let private representativePhrases =
        [ "main point"
          "main points"
          "high level"
          "high-level"
          "what is this"
          "what are these"
          "what does this"
          "what do these" ]

    let private queryHasAnyTerm (terms: string list) (query: string) =
        let queryTerms = Text.terms query |> Set.ofList

        terms |> List.exists (fun term -> queryTerms.Contains(term.ToLowerInvariant()))

    let private queryHasAnyPhrase (phrases: string list) (query: string) =
        let lower = query.ToLowerInvariant()

        phrases |> List.exists (fun phrase -> lower.Contains(phrase))

    let private needsSourceCoverage (query: string) (queryType: QueryType) (sourceCount: int) =
        sourceCount > 1
        && (queryType = QueryType.SectionRetrieval
            || queryHasAnyTerm sourceCoverageTerms query
            || queryHasAnyPhrase representativePhrases query)

    let private needsRepresentativeContext (query: string) (queryType: QueryType) (sourceCount: int) =
        sourceCount > 0
        && (queryType = QueryType.SectionRetrieval
            || queryHasAnyTerm (sourceCoverageTerms @ representativeTerms) query
            || queryHasAnyPhrase representativePhrases query)

    let private firstChunkForSource (source: KnowledgeSource) (chunks: SourceChunk list) =
        chunks
        |> List.filter (fun chunk -> sourceEquals chunk.source source)
        |> List.sortBy (fun chunk -> chunk.index)
        |> List.tryHead

    let private representativeChunks maxResults (retrieval: RetrievalIndex) =
        let sources = retrieval.sources |> List.filter _.enabled

        let chunksBySource =
            sources
            |> List.map (fun source ->
                retrieval.chunks
                |> List.filter (fun chunk -> sourceEquals chunk.source source)
                |> List.sortBy (fun chunk -> chunk.index))

        let maxDepth = chunksBySource |> List.map List.length |> List.fold max 0

        [ for depth in 0 .. maxDepth - 1 do
              for chunks in chunksBySource do
                  match chunks |> List.tryItem depth with
                  | Some chunk ->
                      yield
                          { chunk with
                              score = max chunk.score 0.01f }
                  | None -> () ]
        |> List.truncate maxResults

    let private balanceBySource
        (query: string)
        (queryType: QueryType)
        (maxResults: int)
        (retrieval: RetrievalIndex)
        (ranked: SourceChunk list)
        =
        let sources = retrieval.sources |> List.filter _.enabled
        let ranked = distinctChunks ranked

        let representatives () =
            representativeChunks maxResults retrieval

        if maxResults <= 0 then
            []
        elif List.isEmpty ranked && needsRepresentativeContext query queryType sources.Length then
            representatives ()
        elif not (needsSourceCoverage query queryType sources.Length) then
            ranked |> List.truncate maxResults
        else
            let coverage =
                sources
                |> List.choose (fun source ->
                    ranked
                    |> List.tryFind (fun chunk -> sourceEquals chunk.source source)
                    |> Option.orElse (firstChunkForSource source retrieval.chunks))
                |> List.truncate maxResults

            coverage @ ranked @ representatives ()
            |> distinctChunks
            |> List.truncate maxResults

    let private startsWithTerms (requestedTerms: string list) (candidateTerms: string list) =
        not (List.isEmpty requestedTerms)
        && (candidateTerms |> List.truncate requestedTerms.Length) = requestedTerms

    let private leadingTerms (text: string) =
        text |> Text.normalizeWhitespace |> Text.terms |> List.truncate 8

    let private sectionAnchorBoost requested text =
        match FsColbert.DocumentSections.tryGetHeading text with
        | Some heading when FsColbert.DocumentSections.matches requested heading -> 1.0f
        | _ ->
            let requestedTerms = Text.terms requested

            let candidateTerms =
                match leadingTerms text with
                | "section" :: rest -> rest
                | terms -> terms

            if startsWithTerms requestedTerms candidateTerms then
                0.85f
            else
                0.0f

    let private applySectionBoost sectionName (chunks: SourceChunk list) =
        match sectionName with
        | None -> chunks
        | Some requested ->
            chunks
            |> List.map (fun chunk ->
                let boost = sectionAnchorBoost requested chunk.text

                if boost > 0.0f then
                    { chunk with
                        score = chunk.score + boost }
                else
                    chunk)
            |> List.sortByDescending (fun chunk -> chunk.score)

    let private sectionHeadings (retrieval: RetrievalIndex) =
        seq {
            for chunk in retrieval.chunks do
                match FsColbert.DocumentSections.tryGetHeading chunk.text with
                | Some heading -> heading
                | None -> ()

            for _, index in retrieval.colbertIndices do
                for passage in index.passages do
                    match FsColbert.DocumentSections.tryGetHeading passage.reference.text with
                    | Some heading -> heading
                    | None -> ()
        }
        |> Seq.distinctBy FsColbert.DocumentSections.normalizedName
        |> Seq.toList

    let private tryResolveSectionName retrieval requested =
        sectionHeadings retrieval
        |> List.tryFind (fun heading -> FsColbert.DocumentSections.matches requested heading)

    let private retrievalActionTerms =
        [ "about"
          "can"
          "could"
          "describe"
          "explain"
          "extract"
          "find"
          "give"
          "list"
          "please"
          "provide"
          "section"
          "show"
          "summarise"
          "summarize"
          "summary"
          "tell"
          "the"
          "what"
          "would"
          "you"
          "paper"
          "document"
          "pdf"
          "article"
          "source"
          "file" ]
        |> Set.ofList

    let private distinctNonEmpty (values: string seq) =
        values
        |> Seq.choose Text.notEmpty
        |> Seq.distinctBy _.ToLowerInvariant()
        |> Seq.toList

    let private compactTerms (values: string seq) =
        values
        |> Seq.collect Text.terms
        |> Seq.filter (fun term -> not (retrievalActionTerms.Contains term))
        |> Seq.distinct
        |> Seq.truncate 12
        |> Seq.toList

    let private retrievalTerms (values: string seq) =
        values
        |> Seq.filter (fun term ->
            let key = term.Trim().ToLowerInvariant()
            not (retrievalActionTerms.Contains key))
        |> distinctNonEmpty

    let private cleanupRetrievalTarget (target: string) =
        let withoutSourceTail =
            Regex.Replace(target, @"(?i)\b(?:from|in|of)\s+(?:the\s+)?(?:paper|document|pdf|article|source)\b.*$", "")

        let withoutSourceHead =
            Regex.Replace(withoutSourceTail, @"(?i)^\s*(?:the\s+)?(?:paper|document|pdf|article|source|file)\s+", "")

        Regex.Replace(withoutSourceHead, @"[?.!,;:\s]+$", "").Trim()

    let private tryExtractRetrievalTarget (query: string) =
        let patterns =
            [ @"(?i)^\s*(?:can|could|would)\s+you\s+(?:please\s+)?(?:summari[sz]e|explain|describe|outline|extract|find|show|give|provide|tell\s+me\s+about)\s+(?:the\s+|a\s+|an\s+)?(?<target>[\p{L}\p{N}\s_/\-.'&()]{3,100})\s*[?.!]*\s*$"
              @"(?i)^\s*(?:please\s+)?(?:summari[sz]e|explain|describe|outline|extract|find|show|give|provide|tell\s+me\s+about)\s+(?:the\s+|a\s+|an\s+)?(?<target>[\p{L}\p{N}\s_/\-.'&()]{3,100})\s*[?.!]*\s*$" ]

        patterns
        |> List.tryPick (fun pattern ->
            let m = Regex.Match(query, pattern)

            if m.Success then
                m.Groups["target"].Value |> cleanupRetrievalTarget |> Text.notEmpty
            else
                None)

    let private createLocalExpansionWithProfile profile query =
        if String.IsNullOrWhiteSpace query then
            None
        else
            let processed = QueryPostProcessing.forVoiceLikeRetrievalWithProfile profile query

            let target = tryExtractRetrievalTarget query

            let queryType =
                if Option.isSome target then
                    QueryType.SectionRetrieval
                else
                    QueryType.Question

            let rewrittenQueries =
                match target with
                | Some value -> [ yield value; yield $"section {value}"; yield! processed.rewrittenQueries ]
                | None -> processed.rewrittenQueries

            let baseTerms =
                match target with
                | Some value -> compactTerms [ value; $"section {value}" ]
                | None -> compactTerms [ query ]

            Some
                { terms = Seq.append processed.searchTerms baseTerms |> retrievalTerms |> List.truncate 32
                  rewrittenQueries = distinctNonEmpty rewrittenQueries
                  sectionName = target
                  queryType = queryType }

    let private createLocalExpansion query =
        createLocalExpansionWithProfile QaPlugInProfile.generic query

    let private mergeExpansions localExpansion remoteExpansion =
        match localExpansion, remoteExpansion with
        | None, None -> None
        | Some local, None -> Some local
        | None, Some remote -> Some remote
        | Some local, Some remote ->
            let queryType =
                if local.queryType = QueryType.SectionRetrieval then
                    QueryType.SectionRetrieval
                else
                    remote.queryType

            Some
                { terms = distinctNonEmpty (Seq.append local.terms remote.terms)
                  rewrittenQueries = distinctNonEmpty (Seq.append local.rewrittenQueries remote.rewrittenQueries)
                  sectionName = remote.sectionName |> Option.orElse local.sectionName
                  queryType = queryType }

    let private sanitizeExpansion (expansion: Expansion) =
        { terms = distinctNonEmpty expansion.terms |> List.truncate 12
          rewrittenQueries = distinctNonEmpty expansion.rewrittenQueries |> List.truncate 3
          sectionName = expansion.sectionName |> Option.bind Text.notEmpty
          queryType = expansion.queryType }

    let private canonicalizeSectionTarget (retrieval: RetrievalIndex) (expansion: Expansion) =
        match expansion.sectionName |> Option.bind (tryResolveSectionName retrieval) with
        | None -> expansion
        | Some canonicalSection ->
            { expansion with
                terms =
                    seq {
                        canonicalSection
                        yield! expansion.terms
                    }
                    |> distinctNonEmpty
                    |> List.truncate 12
                rewrittenQueries =
                    seq {
                        canonicalSection
                        $"section {canonicalSection}"
                        yield! expansion.rewrittenQueries
                    }
                    |> distinctNonEmpty
                    |> List.truncate 3
                sectionName = Some canonicalSection }

    let getSynonymsWithProfile
        (profile: QaPlugInProfile)
        (client: IChatClient)
        (report: (string -> unit) option)
        query
        : Async<Expansion option> =
        async {
            if String.IsNullOrWhiteSpace query then
                return None
            else
                try
                    let profile = QaPlugInProfile.sanitize profile

                    let profileHints =
                        let hints = QaPlugInProfile.renderHints profile

                        seq {
                            yield $"Use case: {profile.displayName} ({profile.id})."

                            match profile.description with
                            | Some description -> yield $"Description: {description}"
                            | None -> ()

                            if not (String.IsNullOrWhiteSpace hints) then
                                yield $"Domain hints: {hints}"
                        }
                        |> String.concat "\n"

                    let prompt =
                        $"""
Create compact retrieval signals for searching selected knowledge-source passages.
{profileHints}

Return JSON matching the schema:
- terms: at most 8 content keywords, aliases, or technical terms likely to appear in relevant passages. Avoid generic action words such as summarize, explain, describe, question, answer, paper, document, and PDF.
- rewrittenQueries: 1-2 short retrieval queries that describe the information to find, not the operation to perform. For a named section request, include the canonical target, e.g. "abstract" and "section abstract".
- sectionName: the named section, table, figure, appendix, or other document part being requested, or null when there is no such target.
- queryType: "Question" for factual questions; "SectionRetrieval" for requests to retrieve, summarize, explain, list, or show a named or broad document section.

Do not answer the user. Only provide retrieval signals.

Query: {query}
"""

                    let opts = ChatOptions()
                    opts.ResponseFormat <- ChatResponseFormat.ForJsonSchema<Expansion>()
                    let! response = client.GetResponseAsync<Expansion>(prompt, opts) |> Async.AwaitTask
                    let expansion = sanitizeExpansion response.Result

                    report
                    |> Option.iter (fun r ->
                        let keywords = String.concat ", " expansion.terms
                        let rewrites = String.concat " | " expansion.rewrittenQueries

                        r
                            $"Query type: {expansion.queryType}. Retrieval query: {rewrites}. Expanded keywords: {keywords}")

                    return Some expansion
                with ex ->
                    report |> Option.iter (fun r -> r $"Query expansion failed: {ex.ToString()}")
                    return None
        }

    let getSynonyms (client: IChatClient) (report: (string -> unit) option) query : Async<Expansion option> =
        getSynonymsWithProfile QaPlugInProfile.generic client report query

    let private getSearchWeights =
        function
        | QueryType.Question -> 1.0f, 0.1f
        | QueryType.SectionRetrieval -> 1.0f, 0.35f
        | _ -> 1.0f, 1.0f

    let rankWithProfile
        (profile: QaPlugInProfile)
        (queryExpansionClient: IChatClient option)
        (logExpansions: bool)
        (logChunks: bool)
        (useLexicalFilter: bool)
        (report: string -> unit)
        (query: string)
        (maxResults: int)
        (retrieval: RetrievalIndex)
        : Async<SourceChunk list> =
        async {
            let localExpansion = createLocalExpansionWithProfile profile query

            let! remoteExpansion =
                match queryExpansionClient, useLexicalFilter with
                | Some client, true ->
                    let r = if logExpansions then Some report else None
                    getSynonymsWithProfile profile client r query
                | _ -> async { return None }

            let expansion =
                mergeExpansions localExpansion remoteExpansion
                |> Option.map (canonicalizeSectionTarget retrieval)

            let retrievalQuery =
                expansion
                |> Option.bind (fun e -> e.rewrittenQueries |> List.tryHead)
                |> Option.defaultValue query

            let searchTerms =
                expansion
                |> Option.map (fun e ->
                    [ yield! e.terms
                      match e.sectionName with
                      | Some sectionName -> yield sectionName
                      | None -> () ])
                |> Option.defaultValue []
                |> retrievalTerms

            let queryType =
                expansion
                |> Option.map (fun e -> e.queryType)
                |> Option.defaultValue QueryType.Question

            let lexicalQuery =
                if List.isEmpty searchTerms then
                    retrievalQuery
                else
                    String.concat " " searchTerms

            let sectionName = expansion |> Option.bind (fun e -> e.sectionName)

            let lexicalFallback () =
                let lexicalMaxResults =
                    match queryType with
                    | QueryType.SectionRetrieval -> max maxResults 20
                    | _ -> maxResults

                retrieval.chunks
                |> rankLexically lexicalQuery lexicalMaxResults
                |> applySectionBoost sectionName
                |> balanceBySource query queryType maxResults retrieval

            match retrieval.encoder, retrieval.colbertIndices with
            | Some encoder, indices when not (List.isEmpty indices) ->
                try
                    let denseWeight, lexicalWeight = getSearchWeights queryType

                    let rawMaxResults =
                        match queryType with
                        | QueryType.SectionRetrieval -> max maxResults 20
                        | _ -> maxResults

                    let options =
                        let tunedCandidateLimit = max FsColbert.SearchOptions.defaults.candidateLimit 256

                        { FsColbert.SearchOptions.defaults with
                            maxResults = rawMaxResults
                            candidateLimit = tunedCandidateLimit
                            useLexicalFilter = useLexicalFilter
                            useRRF = true
                            denseWeight = denseWeight
                            lexicalWeight = lexicalWeight }

                    let! allHits =
                        indices
                        |> List.map (fun (_, index) ->
                            FsColbert.Search.queryWithSearchTerms encoder options index retrievalQuery searchTerms)
                        |> Async.Parallel

                    let context =
                        allHits
                        |> Array.collect (List.toArray >> Array.map (hitToChunk retrieval.sources))
                        |> Array.sortByDescending (fun c -> c.score)
                        |> Array.toList
                        |> applySectionBoost sectionName
                        |> balanceBySource query queryType maxResults retrieval

                    if logChunks then
                        for chunk in context do
                            report $"Retrieved chunk: [{chunk.source.DisplayName}] {chunk.text}"

                    return context
                with _ ->
                    return lexicalFallback ()
            | _ -> return lexicalFallback ()
        }

    let rank
        (queryExpansionClient: IChatClient option)
        (logExpansions: bool)
        (logChunks: bool)
        (useLexicalFilter: bool)
        (report: string -> unit)
        (query: string)
        (maxResults: int)
        (retrieval: RetrievalIndex)
        : Async<SourceChunk list> =
        rankWithProfile
            QaPlugInProfile.generic
            queryExpansionClient
            logExpansions
            logChunks
            useLexicalFilter
            report
            query
            maxResults
            retrieval

    let private enabledSources (sources: KnowledgeSource list) =
        sources |> List.filter (fun source -> source.enabled)

    let loadInternalIndex (sources: KnowledgeSource list) : Async<RetrievalIndex * string list> =
        async {
            let sources = enabledSources sources
            let! chunks, errors = loadChunks sources

            return
                { sources = sources
                  chunks = chunks
                  colbertIndices = []
                  encoder = None },
                errors
        }

    let private fsColbertRoot storageRoot =
        let path = Path.Combine(storageRoot, "FsVoice", "FsColbert")
        Directory.CreateDirectory path |> ignore
        path

    let private modelFolder storageRoot =
        Path.Combine(fsColbertRoot storageRoot, "Models", "mxbai-edge-colbert")

    let private indexFolder storageRoot =
        let path = Path.Combine(fsColbertRoot storageRoot, "Indexes")
        Directory.CreateDirectory path |> ignore
        path

    let private prebuiltFolder storageRoot =
        let path = Path.Combine(fsColbertRoot storageRoot, "Prebuilt")
        Directory.CreateDirectory path |> ignore
        path

    let private knownPrebuiltFolders storageRoot =
        [ Path.Combine(storageRoot, "FsVoice", "FsColbert", "Prebuilt")
          Path.Combine(storageRoot, "Speak2Docs", "FsColbert", "Prebuilt") ]
        |> List.distinctBy (fun path -> Path.GetFullPath(path).ToLowerInvariant())

    let private prebuiltManifestPath storageRoot =
        Path.Combine(prebuiltFolder storageRoot, "prebuilt-indexes.installed.json")

    let private prebuiltBundleManifestPath storageRoot =
        Path.Combine(prebuiltFolder storageRoot, "index-bundle.json")

    let clearPersistedIndexes storageRoot =
        async {
            let files =
                Directory.EnumerateFiles(indexFolder storageRoot, "*.fsci") |> Seq.toList

            let errors = ResizeArray<string>()

            let deleted =
                files
                |> List.sumBy (fun path ->
                    try
                        File.Delete path
                        1
                    with ex ->
                        errors.Add $"Unable to delete FsColbert index '{path}': {ex.Message}"
                        0)

            return deleted, List.ofSeq errors
        }

    let private sourceFingerprint (source: KnowledgeSource) =
        let filePart =
            let info = FileInfo source.location

            if info.Exists then
                $"{info.FullName}:{info.Length}:{info.LastWriteTimeUtc.Ticks}"
            else
                source.location

        $"{sourceKindId source.kind}:{filePart}"

    let private hashText (value: string) =
        use sha = SHA256.Create()
        let bytes = Encoding.UTF8.GetBytes value

        sha.ComputeHash bytes
        |> Convert.ToHexString
        |> fun hash -> hash.ToLowerInvariant()

    type private PassageKeywordItem =
        { passageIndex: int
          keywords: string list }

    type private KeywordBatchGeneration =
        | GeneratedKeywords of PassageKeywordItem list
        | RejectedKeywordSchema of string

    type private KeywordCacheRecord =
        { key: string
          sourceFingerprint: string
          passageIndex: int
          textHash: string
          modelId: string
          schemaVersion: string
          profileFingerprint: string
          keywords: string list }

    let private keywordCacheFolder storageRoot =
        let path = Path.Combine(fsColbertRoot storageRoot, "KeywordCache")
        Directory.CreateDirectory path |> ignore
        path

    let private keywordCachePath storageRoot sourceFingerprint (options: KeywordGenerationOptions) =
        let fileName =
            [ yield sourceFingerprint
              yield options.modelId
              yield options.schemaVersion
              yield QaPlugInProfile.fingerprint options.plugInProfile

              if not (String.IsNullOrWhiteSpace options.plugInFingerprint) then
                  yield options.plugInFingerprint ]
            |> String.concat "\n"
            |> hashText

        Path.Combine(keywordCacheFolder storageRoot, $"{fileName}.jsonl")

    let private sanitizeKeywordOptions (options: KeywordGenerationOptions) : KeywordGenerationOptions =
        { options with
            batchSize = max 1 options.batchSize
            parallelism = max 1 options.parallelism
            maxOutputTokens = max 1024 options.maxOutputTokens
            modelId =
                options.modelId
                |> Text.notEmpty
                |> Option.defaultValue KeywordGenerationOptions.defaults.modelId
            schemaVersion =
                options.schemaVersion
                |> Text.notEmpty
                |> Option.defaultValue KeywordGenerationOptions.defaults.schemaVersion
            cancellationToken = options.cancellationToken
            plugInProfile = QaPlugInProfile.sanitize options.plugInProfile
            plugInFingerprint = options.plugInFingerprint |> Text.notEmpty |> Option.defaultValue "" }

    let private keywordCacheKey sourceFingerprint (options: KeywordGenerationOptions) passageIndex textHash =
        [ yield sourceFingerprint
          yield string passageIndex
          yield textHash
          yield options.modelId
          yield options.schemaVersion
          yield QaPlugInProfile.fingerprint options.plugInProfile

          if not (String.IsNullOrWhiteSpace options.plugInFingerprint) then
              yield options.plugInFingerprint ]
        |> String.concat "\n"
        |> hashText

    let private cleanKeywords maxValues (values: string list) =
        values
        |> List.choose (Text.normalizeWhitespace >> Text.notEmpty)
        |> List.distinctBy _.ToLowerInvariant()
        |> List.truncate maxValues

    let private strictStructuredOutputKey = "strict"

    let private strictJsonSchemaResponseFormat (schema: string) (name: string) (description: string) =
        use document = JsonDocument.Parse schema

        ChatResponseFormat.ForJsonSchema(document.RootElement.Clone(), name, description)

    let private applyStrictStructuredOutput (options: ChatOptions) =
        if isNull options.AdditionalProperties then
            options.AdditionalProperties <- AdditionalPropertiesDictionary()

        options.AdditionalProperties[strictStructuredOutputKey] <- box true

    let private exceptionDetails (ex: exn) =
        let rec collect (current: exn) =
            seq {
                if not (isNull current) then
                    yield current.GetType().FullName
                    yield current.Message
                    yield! collect current.InnerException
            }

        collect ex |> Seq.choose Text.notEmpty |> String.concat "\n"

    let private isInvalidJsonSchemaError (ex: exn) =
        let details = exceptionDetails ex |> fun value -> value.ToLowerInvariant()

        details.Contains("invalid_json_schema")
        || ((details.Contains("http 400")
             || details.Contains("status 400")
             || details.Contains("bad request")
             || details.Contains("invalid request"))
            && (details.Contains("json_schema")
                || details.Contains("response_format")
                || details.Contains("structured output")
                || details.Contains("schema")))

    type private PassageKeywordBatch =
        { items: PassageKeywordItem list option }

    let private readKeywordBatch (text: string) =
        tryDeserialize<PassageKeywordBatch> text
        |> Option.bind _.items
        |> Option.defaultValue []
        |> List.map (fun item ->
            { item with
                keywords = cleanKeywords 16 item.keywords })

    let private tryReadKeywordCacheRecord (line: string) =
        tryDeserialize<KeywordCacheRecord> line
        |> Option.map (fun record ->
            { record with
                keywords = cleanKeywords 16 record.keywords })

    let private loadKeywordCache path sourceFingerprint (options: KeywordGenerationOptions) =
        if File.Exists path then
            let profileFingerprint = QaPlugInProfile.fingerprint options.plugInProfile

            File.ReadLines path
            |> Seq.choose tryReadKeywordCacheRecord
            |> Seq.filter (fun record ->
                record.sourceFingerprint = sourceFingerprint
                && record.modelId = options.modelId
                && record.schemaVersion = options.schemaVersion
                && record.profileFingerprint = profileFingerprint)
            |> Seq.map (fun record -> record.key, record)
            |> Map.ofSeq
        else
            Map.empty

    let private appendKeywordCache path (records: KeywordCacheRecord list) =
        if not (List.isEmpty records) then
            Directory.CreateDirectory(Path.GetDirectoryName(Path.GetFullPath path))
            |> ignore

            use writer = new StreamWriter(path, true, Encoding.UTF8)

            for record in records do
                let line =
                    JsonSerializer.Serialize(
                        {| key = record.key
                           sourceFingerprint = record.sourceFingerprint
                           passageIndex = record.passageIndex
                           textHash = record.textHash
                           modelId = record.modelId
                           schemaVersion = record.schemaVersion
                           profileFingerprint = record.profileFingerprint
                           keywords = record.keywords |}
                    )

                writer.WriteLine line

    let private promptPassageText (text: string) =
        let normalized = Text.normalizeWhitespace text

        if normalized.Length <= 1000 then
            normalized
        else
            normalized.Substring(0, 1000)

    let private keywordPrompt (options: KeywordGenerationOptions) (batch: FsColbert.PassageRef list) =
        let payload =
            batch
            |> List.map (fun passage ->
                {| passageIndex = passage.index
                   text = promptPassageText passage.text |})
            |> fun items -> JsonSerializer.Serialize items

        let profile = QaPlugInProfile.sanitize options.plugInProfile

        let profileText =
            seq {
                yield $"Use case: {profile.displayName} ({profile.id})."

                match profile.description with
                | Some description -> yield $"Description: {description}"
                | None -> ()

                let hints = QaPlugInProfile.renderHints profile

                if not (String.IsNullOrWhiteSpace hints) then
                    yield $"Domain hints: {hints}"

                match profile.keywordInstruction with
                | Some instruction -> yield $"PlugIn keyword guidance: {instruction}"
                | None -> ()
            }
            |> String.concat "\n"

        $"""
Create compact lexical search keywords for knowledge-source retrieval passages.
{profileText}

Return only a JSON object with one property, "items", whose value is an array.
Each item must have:
- passageIndex: the same integer passageIndex.
- keywords: 6-14 short phrases, synonyms, abbreviations, product or feature names, entities, and likely user wording.

Rules:
- Use only facts supported by the passage text.
- Prefer terms that may be useful for exact lexical search and may not appear literally in the passage.
- Do not include markdown, explanations, answer prose, or fields other than passageIndex and keywords.

Passages:
{payload}
"""

    let private keywordResponseFormat () =
        let schema =
            """
{
  "type": "object",
  "additionalProperties": false,
  "properties": {
    "items": {
      "type": "array",
      "items": {
        "type": "object",
        "additionalProperties": false,
        "properties": {
          "passageIndex": { "type": "integer" },
          "keywords": {
            "type": "array",
            "items": { "type": "string" }
          }
        },
        "required": [ "passageIndex", "keywords" ]
      }
    }
  },
  "required": [ "items" ]
}
"""

        strictJsonSchemaResponseFormat schema "passage_keyword_batch" "Keyword metadata for a batch of passages."

    let private normalizeKeywordBatch (batch: FsColbert.PassageRef list) responseText =
        let requested = batch |> List.map _.index |> Set.ofList

        responseText
        |> readKeywordBatch
        |> List.filter (fun item -> Set.contains item.passageIndex requested)
        |> List.map (fun item ->
            { item with
                keywords = cleanKeywords 16 item.keywords })
        |> List.filter (fun item -> not (List.isEmpty item.keywords))
        |> List.distinctBy _.passageIndex

    let private generateKeywordBatchWith
        (responseFormat: ChatResponseFormat option)
        strict
        (client: IChatClient)
        (options: KeywordGenerationOptions)
        (batch: FsColbert.PassageRef list)
        =
        async {
            let opts = ChatOptions()
            opts.MaxOutputTokens <- Nullable options.maxOutputTokens

            responseFormat |> Option.iter (fun format -> opts.ResponseFormat <- format)

            if strict then
                applyStrictStructuredOutput opts

            let reasoning = ReasoningOptions()
            reasoning.Effort <- Nullable ReasoningEffort.Low
            reasoning.Output <- Nullable ReasoningOutput.None
            opts.Reasoning <- reasoning

            let messages = [ ChatMessage(ChatRole.User, keywordPrompt options batch) ]

            let parentToken =
                options.cancellationToken |> Option.defaultValue CancellationToken.None

            use timeout = CancellationTokenSource.CreateLinkedTokenSource parentToken

            timeout.CancelAfter(DEFAULT_KEYWORD_METADATA_BATCH_TIMEOUT_MS)

            let! response = client.GetResponseAsync(messages, opts, timeout.Token) |> Async.AwaitTask

            return normalizeKeywordBatch batch response.Text
        }

    let private generateKeywordBatch client options batch =
        generateKeywordBatchWith (Some(keywordResponseFormat ())) true client options batch

    let private generateKeywordBatchJsonMode client options batch =
        generateKeywordBatchWith (Some ChatResponseFormat.Json) false client options batch

    let private generateKeywordBatchPlainJson client options batch =
        generateKeywordBatchWith None false client options batch

    let rec private generateKeywordBatchRecoverWith
        (generateBatch:
            IChatClient -> KeywordGenerationOptions -> FsColbert.PassageRef list -> Async<PassageKeywordItem list>)
        report
        client
        options
        (batch: FsColbert.PassageRef list)
        =
        async {
            try
                let! generated = generateBatch client options batch
                let generatedIndexes = generated |> List.map _.passageIndex |> Set.ofList

                let missing =
                    batch
                    |> List.filter (fun passage -> not (Set.contains passage.index generatedIndexes))

                if List.isEmpty missing || batch.Length <= 1 then
                    return GeneratedKeywords generated
                else
                    let! recovered =
                        missing
                        |> List.map (fun passage ->
                            generateKeywordBatchRecoverWith generateBatch report client options [ passage ])
                        |> Async.Parallel

                    match
                        recovered
                        |> Array.tryPick (function
                            | RejectedKeywordSchema reason -> Some reason
                            | _ -> None)
                    with
                    | Some reason -> return RejectedKeywordSchema reason
                    | None ->
                        let recoveredKeywords =
                            recovered
                            |> Array.collect (function
                                | GeneratedKeywords items -> List.toArray items
                                | RejectedKeywordSchema _ -> [||])
                            |> Array.toList

                        return GeneratedKeywords(generated @ recoveredKeywords)
            with ex ->
                if
                    (match ex with
                     | :? OperationCanceledException -> true
                     | _ -> false)
                    && (options.cancellationToken |> Option.exists _.IsCancellationRequested)
                then
                    return raise ex
                elif isInvalidJsonSchemaError ex then
                    return RejectedKeywordSchema(ex.Message)
                elif batch.Length <= 1 then
                    match batch with
                    | passage :: _ ->
                        report
                            $"Keyword generation failed for {passage.sourceDisplayName} chunk {passage.index}: {ex.Message}"
                    | [] -> ()

                    return GeneratedKeywords []
                else
                    let half = max 1 (batch.Length / 2)
                    let left, right = batch |> List.splitAt half

                    let! recovered =
                        [ generateKeywordBatchRecoverWith generateBatch report client options left
                          generateKeywordBatchRecoverWith generateBatch report client options right ]
                        |> Async.Parallel

                    match
                        recovered
                        |> Array.tryPick (function
                            | RejectedKeywordSchema reason -> Some reason
                            | _ -> None)
                    with
                    | Some reason -> return RejectedKeywordSchema reason
                    | None ->
                        return
                            recovered
                            |> Array.collect (function
                                | GeneratedKeywords items -> List.toArray items
                                | RejectedKeywordSchema _ -> [||])
                            |> Array.toList
                            |> GeneratedKeywords
        }

    let private generateKeywordBatchRecover report client options batch =
        generateKeywordBatchRecoverWith generateKeywordBatch report client options batch

    let private generateKeywordBatchJsonModeRecover report client options batch =
        generateKeywordBatchRecoverWith generateKeywordBatchJsonMode report client options batch

    let private generateKeywordBatchPlainJsonRecover report client options batch =
        generateKeywordBatchRecoverWith generateKeywordBatchPlainJson report client options batch

    let private mapAsyncThrottled degree work items =
        items
        |> AsyncSeq.ofSeq
        |> AsyncSeq.mapAsyncParallelThrottled (max 1 degree) work
        |> AsyncSeq.toListAsync

    let private generateKeywordBatchesWithProgress
        report
        sourceDisplayName
        stageLabel
        degree
        generate
        (batches: FsColbert.PassageRef list list)
        =
        let total = batches.Length
        let completed = ref 0

        batches
        |> List.mapi (fun index batch -> index + 1, batch)
        |> mapAsyncThrottled degree (fun (batchNo, batch) ->
            async {
                let firstIndex = batch |> List.map _.index |> List.min
                let lastIndex = batch |> List.map _.index |> List.max

                report
                    $"Keyword metadata {stageLabel} for {sourceDisplayName}: starting chunk batch {batchNo}/{total} (passages {firstIndex}-{lastIndex})."

                let! result = generate batch
                let finished = Interlocked.Increment completed

                report
                    $"Keyword metadata {stageLabel} for {sourceDisplayName}: completed {finished}/{total} chunk batch(es)."

                return result
            })

    let private keywordMetadataFingerprint (options: KeywordGenerationOptions) (passages: FsColbert.PassageRef list) =
        let metadata =
            passages
            |> List.map (fun passage ->
                {| passageIndex = passage.index
                   textHash = hashText passage.text
                   keywordsHash = passage.keywords |> String.concat "\n" |> hashText |})
            |> fun items -> JsonSerializer.Serialize items

        [ yield "keywords=enabled"
          yield $"keywordModel={options.modelId}"
          yield $"keywordSchema={options.schemaVersion}"
          yield $"plugInProfile={options.plugInProfile.id}"
          yield $"plugInProfileHash={QaPlugInProfile.fingerprint options.plugInProfile}"
          if not (String.IsNullOrWhiteSpace options.plugInFingerprint) then
              yield $"plugInDefinitionHash={options.plugInFingerprint}"
          yield $"keywordMetadataHash={hashText metadata}"
          yield $"tfidfTextWeight={FsColbert.TfidfOptions.defaults.textWeight}"
          yield $"tfidfKeywordWeight={FsColbert.TfidfOptions.defaults.keywordWeight}" ]
        |> String.concat "\n"

    let private disabledKeywordFingerprint =
        [ "keywords=disabled"
          $"tfidfTextWeight={FsColbert.TfidfOptions.defaults.textWeight}"
          $"tfidfKeywordWeight={FsColbert.TfidfOptions.defaults.keywordWeight}" ]
        |> String.concat "\n"

    let internal attachKeywords
        (storageRoot: string)
        (report: string -> unit)
        (keywordOptions: KeywordGenerationOptions)
        (source: KnowledgeSource)
        (passages: FsColbert.PassageRef list)
        =
        async {
            let options = sanitizeKeywordOptions keywordOptions

            let cancellationToken =
                options.cancellationToken |> Option.defaultValue CancellationToken.None

            throwIfCancellationRequested cancellationToken

            if not options.enabled then
                return passages, disabledKeywordFingerprint
            else
                let sourceFingerprintValue = sourceFingerprint source
                let profileFingerprint = QaPlugInProfile.fingerprint options.plugInProfile

                let cachePath = keywordCachePath storageRoot sourceFingerprintValue options

                throwIfCancellationRequested cancellationToken
                let cached = loadKeywordCache cachePath sourceFingerprintValue options
                throwIfCancellationRequested cancellationToken

                let passageKeys =
                    passages
                    |> List.map (fun passage ->
                        let textHash = hashText passage.text

                        passage, textHash, keywordCacheKey sourceFingerprintValue options passage.index textHash)

                let missing =
                    passageKeys
                    |> List.choose (fun (passage, textHash, key) ->
                        if Map.containsKey key cached then
                            None
                        else
                            Some(passage, textHash, key))

                let! generatedRecords =
                    match missing, options.client with
                    | [], _ -> async.Return []
                    | _, None ->
                        report
                            $"Keyword cache is missing {missing.Length}/{passages.Length} passage(s) for {source.DisplayName}; using cached keyword metadata only."

                        async.Return []
                    | _, Some client ->
                        async {
                            report
                                $"Generating keyword metadata for {missing.Length}/{passages.Length} passage(s) in {source.DisplayName}."

                            let batches =
                                missing
                                |> List.map (fun (passage, _, _) -> passage)
                                |> List.chunkBySize options.batchSize

                            throwIfCancellationRequested cancellationToken

                            let! generated =
                                generateKeywordBatchesWithProgress
                                    report
                                    source.DisplayName
                                    "structured generation"
                                    options.parallelism
                                    (generateKeywordBatchRecover report client options)
                                    batches

                            throwIfCancellationRequested cancellationToken

                            let rejectedBatches =
                                List.zip batches generated
                                |> List.choose (fun (batch, outcome) ->
                                    match outcome with
                                    | RejectedKeywordSchema _ -> Some batch
                                    | GeneratedKeywords _ -> None)

                            let! fallbackGenerated =
                                if List.isEmpty rejectedBatches then
                                    async.Return []
                                else
                                    async {
                                        report
                                            "OpenAI rejected the structured keyword schema; retrying keyword generation in JSON mode."

                                        return!
                                            generateKeywordBatchesWithProgress
                                                report
                                                source.DisplayName
                                                "JSON-mode fallback"
                                                options.parallelism
                                                (generateKeywordBatchJsonModeRecover report client options)
                                                rejectedBatches
                                    }

                            let rejectedFallbackBatches =
                                List.zip rejectedBatches fallbackGenerated
                                |> List.choose (fun (batch, outcome) ->
                                    match outcome with
                                    | RejectedKeywordSchema _ -> Some batch
                                    | GeneratedKeywords _ -> None)

                            let! plainGenerated =
                                if List.isEmpty rejectedFallbackBatches then
                                    async.Return []
                                else
                                    async {
                                        report
                                            "OpenAI rejected keyword JSON mode; retrying keyword generation without response formatting."

                                        return!
                                            generateKeywordBatchesWithProgress
                                                report
                                                source.DisplayName
                                                "plain-JSON fallback"
                                                options.parallelism
                                                (generateKeywordBatchPlainJsonRecover report client options)
                                                rejectedFallbackBatches
                                    }

                            let generated = generated @ fallbackGenerated @ plainGenerated
                            throwIfCancellationRequested cancellationToken

                            let schemaRejected =
                                plainGenerated
                                |> List.exists (function
                                    | RejectedKeywordSchema _ -> true
                                    | GeneratedKeywords _ -> false)

                            if schemaRejected then
                                report
                                    "OpenAI rejected keyword schema and response-format fallbacks; indexing will continue without generated keywords."

                            let generatedByIndex =
                                generated
                                |> List.collect (function
                                    | GeneratedKeywords items -> items
                                    | RejectedKeywordSchema _ -> [])
                                |> List.map (fun item -> item.passageIndex, item.keywords)
                                |> Map.ofList

                            return
                                missing
                                |> List.choose (fun (passage, textHash, key) ->
                                    generatedByIndex
                                    |> Map.tryFind passage.index
                                    |> Option.map (fun keywords ->
                                        { key = key
                                          sourceFingerprint = sourceFingerprintValue
                                          passageIndex = passage.index
                                          textHash = textHash
                                          modelId = options.modelId
                                          schemaVersion = options.schemaVersion
                                          profileFingerprint = profileFingerprint
                                          keywords = keywords }))
                        }

                throwIfCancellationRequested cancellationToken
                appendKeywordCache cachePath generatedRecords
                throwIfCancellationRequested cancellationToken

                let allRecords =
                    generatedRecords
                    |> List.fold (fun records record -> Map.add record.key record records) cached

                let enriched =
                    passageKeys
                    |> List.map (fun (passage, _, key) ->
                        let keywords =
                            seq {
                                yield! passage.keywords

                                match allRecords |> Map.tryFind key with
                                | Some record -> yield! record.keywords
                                | None -> ()
                            }
                            |> Seq.toList
                            |> cleanKeywords 32

                        { passage with keywords = keywords })

                return enriched, keywordMetadataFingerprint options enriched
        }

    let sourceParsingFingerprint (pdfOptions: PdfIngestionOptions) (source: KnowledgeSource) =
        match source.kind with
        | Pdf ->
            let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

            let doclingOptions =
                { DoclingHybrid.currentDefaultOptions () with
                    enableLayoutAnalysis =
                        match pdfOptions.parsingMode with
                        | PdfParsingMode.Hybrid -> true
                        | PdfParsingMode.HybridWithoutLayout
                        | PdfParsingMode.Legacy -> false
                    visualDescriptions = pdfOptions.visualDescriptions }

            [ yield PdfParsingModes.indexFingerprint pdfOptions.parsingMode
              yield PdfIngestionOptions.visualFingerprint pdfOptions

              match pdfOptions.parsingMode with
              | PdfParsingMode.Legacy -> ()
              | PdfParsingMode.Hybrid
              | PdfParsingMode.HybridWithoutLayout -> yield DoclingHybrid.parserRuntimeFingerprint doclingOptions ]
            |> String.concat "\n"
        | Markdown -> "parser=markdown"
        | Json -> "parser=json"

    let private sourceIndexPathWithOptions storageRoot pdfOptions (source: KnowledgeSource) keywordFingerprint =
        let options = FsColbert.ChunkOptions.fsKameDefaults
        let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

        let fingerprint =
            [ yield $"model={FsColbert.ModelCatalog.mxbaiEdgeColbertInt8.id}"
              yield $"pdfIndexVersion={pdfIndexVersion}"
              yield sourceParsingFingerprint pdfOptions source
              yield $"chunk={options.maxChars}:{options.overlapChars}:{options.minChars}"
              yield keywordFingerprint
              yield sourceFingerprint source ]
            |> String.concat "\n"

        Path.Combine(indexFolder storageRoot, $"{hashText fingerprint}.fsci")

    let private sourceIndexPath storageRoot pdfParsingMode source keywordFingerprint =
        sourceIndexPathWithOptions storageRoot (PdfIngestionOptions.create pdfParsingMode) source keywordFingerprint

    let private indexPath storageRoot sources =
        let options = FsColbert.ChunkOptions.fsKameDefaults

        let fingerprint =
            [ yield $"model={FsColbert.ModelCatalog.mxbaiEdgeColbertInt8.id}"
              yield $"pdfIndexVersion={pdfIndexVersion}"
              yield $"chunk={options.maxChars}:{options.overlapChars}:{options.minChars}"
              yield! (sources |> List.map sourceFingerprint |> List.sort) ]
            |> String.concat "\n"

        Path.Combine(indexFolder storageRoot, $"{hashText fingerprint}.fsci")

    let private tryLoadPersistedIndex path = FsColbert.IndexPersistence.tryLoad path

    let private indexMetadataPath (indexPath: string) = $"{indexPath}.metadata.json"

    let private sourceKindFingerprint (source: KnowledgeSource) =
        match source.kind with
        | Pdf -> "pdf"
        | Markdown -> "markdown"
        | Json -> "json"

    let private writeIndexMetadataWithOptions indexPath (pdfOptions: PdfIngestionOptions) source keywordFingerprint =
        try
            let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

            let metadata =
                { sourceFingerprint = sourceFingerprint source
                  sourceLocation = source.location
                  sourceDisplayName = source.DisplayName
                  sourceKind = sourceKindFingerprint source
                  parserFingerprint = Some(sourceParsingFingerprint pdfOptions source)
                  pdfParsingMode =
                    match source.kind with
                    | Pdf -> Some(PdfParsingModes.fingerprint pdfOptions.parsingMode)
                    | Markdown
                    | Json -> None
                  keywordFingerprint = keywordFingerprint
                  createdAtUtc = DateTimeOffset.UtcNow }

            let json = JsonSerializer.Serialize(metadata, jsonOptions)
            File.WriteAllText(indexMetadataPath indexPath, json)
        with _ ->
            ()

    let private writeIndexMetadata indexPath pdfParsingMode source keywordFingerprint =
        writeIndexMetadataWithOptions indexPath (PdfIngestionOptions.create pdfParsingMode) source keywordFingerprint

    let private tryReadIndexMetadata indexPath =
        try
            let metadataPath = indexMetadataPath indexPath

            if File.Exists metadataPath then
                JsonSerializer.Deserialize<PersistedIndexMetadata>(File.ReadAllText metadataPath, jsonOptions)
                |> Option.ofObj
            else
                None
        with _ ->
            None

    let private tryLoadPrebuiltManifestFromFolder folder =
        try
            let path = Path.Combine(folder, "prebuilt-indexes.installed.json")

            if File.Exists path then
                JsonSerializer.Deserialize<InstalledPrebuiltIndex array>(File.ReadAllText path)
                |> Option.ofObj
                |> Option.map Array.toList
                |> Option.defaultValue []
            else
                []
        with _ ->
            []

    let private tryLoadPrebuiltManifest storageRoot =
        knownPrebuiltFolders storageRoot
        |> List.collect tryLoadPrebuiltManifestFromFolder

    let private bindIndexToSource (source: KnowledgeSource) (index: FsColbert.ColbertIndex) =
        let passages =
            index.passages
            |> List.map (fun passage ->
                { passage with
                    reference =
                        { passage.reference with
                            sourceId = source.location
                            sourceDisplayName = source.DisplayName
                            sourceLocation = source.location } })

        { index with passages = passages }

    type private PersistedIndexCandidate =
        { path: string
          index: FsColbert.ColbertIndex
          keywordCount: int
          isExactFingerprint: bool
          hasMetadata: bool
          parserMatches: bool
          modifiedTicks: int64 }

    let private indexKeywordCount (index: FsColbert.ColbertIndex) =
        index.passages
        |> List.sumBy (fun passage ->
            passage.reference.keywords
            |> Option.ofObj
            |> Option.map List.length
            |> Option.defaultValue 0)

    let private indexMatchesSource (source: KnowledgeSource) (index: FsColbert.ColbertIndex) =
        index.passages
        |> List.exists (fun passage ->
            String.Equals(passage.reference.sourceLocation, source.location, StringComparison.OrdinalIgnoreCase)
            || String.Equals(passage.reference.sourceId, source.location, StringComparison.OrdinalIgnoreCase))

    let private persistedIndexCandidate pdfOptions source exactPath path =
        let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

        match tryLoadPersistedIndex path with
        | Ok(Some index) when indexMatchesSource source index ->
            let metadata = tryReadIndexMetadata path

            let parserMatches =
                match source.kind, metadata with
                | Pdf, Some metadata ->
                    metadata.parserFingerprint
                    |> Option.exists (fun value ->
                        String.Equals(value, sourceParsingFingerprint pdfOptions source, StringComparison.Ordinal))
                | Pdf, None -> false
                | Markdown, _
                | Json, _ -> true

            let sourceMatches =
                match metadata with
                | Some metadata ->
                    String.Equals(metadata.sourceFingerprint, sourceFingerprint source, StringComparison.Ordinal)
                    || String.Equals(metadata.sourceLocation, source.location, StringComparison.OrdinalIgnoreCase)
                | None -> true

            if sourceMatches then
                let index = bindIndexToSource source index

                Some
                    { path = path
                      index = index
                      keywordCount = indexKeywordCount index
                      isExactFingerprint = String.Equals(path, exactPath, StringComparison.OrdinalIgnoreCase)
                      hasMetadata = metadata.IsSome
                      parserMatches = parserMatches
                      modifiedTicks =
                        try
                            File.GetLastWriteTimeUtc(path).Ticks
                        with _ ->
                            0L }
            else
                None
        | Ok _ -> None
        | Error _ -> None

    let private tryLoadBestPersistedIndexWithOptions storageRoot pdfOptions source exactPath =
        try
            let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

            Directory.EnumerateFiles(indexFolder storageRoot, "*.fsci")
            |> Seq.choose (persistedIndexCandidate pdfOptions source exactPath)
            |> Seq.sortByDescending (fun candidate ->
                candidate.parserMatches,
                candidate.hasMetadata,
                candidate.keywordCount,
                candidate.isExactFingerprint,
                candidate.modifiedTicks)
            |> Seq.tryHead
            |> Ok
        with ex ->
            Error ex.Message

    let private tryLoadBestPersistedIndex storageRoot pdfParsingMode source exactPath =
        tryLoadBestPersistedIndexWithOptions storageRoot (PdfIngestionOptions.create pdfParsingMode) source exactPath

    let private loadPersistedIndexWithoutParsingWithOptions
        storageRoot
        report
        (pdfOptions: PdfIngestionOptions)
        source
        =
        let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

        match tryLoadBestPersistedIndexWithOptions storageRoot pdfOptions source "" with
        | Ok(Some candidate) when source.kind <> Pdf || candidate.parserMatches ->
            report $"Loaded FsColbert index for {source.DisplayName}; indexKeywords={candidate.keywordCount}."

            Ok(Some candidate.index)
        | Ok(Some candidate) ->
            let metadataDescription =
                if candidate.hasMetadata then
                    "older or different parser metadata"
                else
                    "no parser metadata"

            report
                $"Loaded FsColbert index for {source.DisplayName} with {metadataDescription}; reprocess this source to refresh with the current {PdfParsingModes.displayName pdfOptions.parsingMode} PDF parser and visual-description settings."

            Ok(Some candidate.index)
        | Ok None -> Ok None
        | Error err -> Error err

    let private loadPersistedIndexWithoutParsing storageRoot report pdfParsingMode source =
        loadPersistedIndexWithoutParsingWithOptions
            storageRoot
            report
            (PdfIngestionOptions.create pdfParsingMode)
            source

    let private prebuiltBundleSourceMatches (source: KnowledgeSource) (entry: FsColbert.LoadedIndexBundleEntry) =
        let candidates =
            [ yield entry.source.sourceId
              yield entry.source.sourceDisplayName

              match entry.source.sourceLocation with
              | Some location -> yield location
              | None -> () ]

        candidates
        |> List.exists (fun value ->
            String.Equals(value, source.location, StringComparison.OrdinalIgnoreCase)
            || String.Equals(value, source.DisplayName, StringComparison.OrdinalIgnoreCase))

    let private tryLoadPrebuiltBundleIndexFromFolder folder (source: KnowledgeSource) =
        let manifestPath = Path.Combine(folder, "index-bundle.json")

        if not (File.Exists manifestPath) then
            Ok None
        else
            match
                FsColbert.IndexBundle.loadCompatible FsColbert.IndexBundleCompatibility.fsKameDefaults manifestPath
            with
            | Error errors -> Error(String.concat Environment.NewLine errors)
            | Ok bundle ->
                bundle.indexes
                |> List.tryFind (prebuiltBundleSourceMatches source)
                |> Option.map (fun entry -> bindIndexToSource source entry.index)
                |> Ok

    let private tryLoadPrebuiltBundleIndex storageRoot (source: KnowledgeSource) =
        let rec loop folders =
            match folders with
            | [] -> Ok None
            | folder :: remaining ->
                match tryLoadPrebuiltBundleIndexFromFolder folder source with
                | Ok(Some index) -> Ok(Some index)
                | Ok None -> loop remaining
                | Error err -> Error err

        loop (knownPrebuiltFolders storageRoot)

    let private tryLoadLegacyPrebuiltIndex storageRoot (source: KnowledgeSource) =
        tryLoadPrebuiltManifest storageRoot
        |> List.tryFind (fun item ->
            String.Equals(item.storedPath, source.location, StringComparison.OrdinalIgnoreCase)
            && File.Exists item.indexPath)
        |> function
            | None -> Ok None
            | Some item ->
                match tryLoadPersistedIndex item.indexPath with
                | Ok(Some index) -> Ok(Some(bindIndexToSource source index))
                | Ok None -> Ok None
                | Error err -> Error err

    let tryLoadPrebuiltIndex storageRoot (source: KnowledgeSource) =
        match tryLoadLegacyPrebuiltIndex storageRoot source with
        | Ok(Some index) -> Ok(Some index)
        | Error err -> Error err
        | Ok None -> tryLoadPrebuiltBundleIndex storageRoot source

    let private vectorValueSample maxValues (embedding: FsColbert.MultiVector) =
        embedding.vectors |> Array.truncate maxValues |> Array.toList

    let private toIndexPreviewRecord (passage: FsColbert.IndexedPassage) =
        { index = passage.reference.index
          text = passage.reference.text
          keywords = passage.reference.keywords |> Option.ofObj |> Option.defaultValue []
          terms = passage.terms |> Set.toList |> List.sort
          vector =
            { tokenCount = passage.embedding.tokenCount
              embeddingDim = passage.embedding.embeddingDim
              valueSample = vectorValueSample 8 passage.embedding } }

    let private randomSample maxRecords (items: 'T list) =
        if maxRecords <= 0 then
            []
        elif items.Length <= maxRecords then
            items
        else
            let random = Random()

            items |> List.sortBy (fun _ -> random.Next()) |> List.truncate maxRecords

    let createIndexPreview maxRecords source (index: FsColbert.ColbertIndex) =
        let records =
            index.passages |> randomSample maxRecords |> List.map toIndexPreviewRecord

        { source = source
          totalChunks = index.passages.Length
          sampledCount = records.Length
          records = records }

    let tryLoadIndexForPreviewWithOptions
        storageRoot
        report
        (pdfOptions: PdfIngestionOptions)
        (source: KnowledgeSource)
        =
        let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

        let prebuilt = tryLoadPrebuiltIndex storageRoot source

        match prebuilt with
        | Ok(Some index) ->
            report $"Loaded prebuilt FsColbert index preview for {source.DisplayName}."
            Ok index
        | Error err -> Error err
        | Ok None ->
            match tryLoadBestPersistedIndexWithOptions storageRoot pdfOptions source "" with
            | Ok(Some candidate) when source.kind <> Pdf || candidate.parserMatches ->
                report $"Loaded persisted FsColbert index preview for {source.DisplayName}."
                Ok candidate.index
            | Ok(Some candidate) ->
                let metadataDescription =
                    if candidate.hasMetadata then
                        "older or different parser metadata"
                    else
                        "no parser metadata"

                report
                    $"Loaded persisted FsColbert index preview for {source.DisplayName} with {metadataDescription}; reprocess this source to refresh with the current {PdfParsingModes.displayName pdfOptions.parsingMode} PDF parser and visual-description settings."

                Ok candidate.index
            | Ok None -> Error $"No FsColbert index is available for {source.DisplayName}."
            | Error err -> Error err

    let tryLoadIndexForPreview storageRoot report pdfParsingMode source =
        tryLoadIndexForPreviewWithOptions storageRoot report (PdfIngestionOptions.create pdfParsingMode) source

    let loadIndexPreviewWithOptions storageRoot report pdfOptions maxRecords source =
        tryLoadIndexForPreviewWithOptions storageRoot report pdfOptions source
        |> Result.map (createIndexPreview maxRecords source)

    let loadIndexPreview storageRoot report pdfParsingMode maxRecords source =
        tryLoadIndexForPreview storageRoot report pdfParsingMode source
        |> Result.map (createIndexPreview maxRecords source)

    let private loadEncoder storageRoot =
        async {
            match cachedEncoder with
            | Some e -> return e
            | None ->
                use client = new HttpClient()

                let! files =
                    FsColbert.ModelCatalog.ensureDownloadedAsync
                        client
                        (modelFolder storageRoot)
                        FsColbert.ModelCatalog.mxbaiEdgeColbertInt8

                let encoder = FsColbert.OnnxColbertEncoder.Load files
                cachedEncoder <- Some encoder
                return encoder
        }

    let private buildIndexFromChunks
        report
        encoder
        (passages: FsColbert.PassageRef list)
        path
        (cancellationToken: CancellationToken)
        =
        async {
            throwIfCancellationRequested cancellationToken
            let mutable lastReported = -1

            let progress (update: FsColbert.IndexProgress) =
                if update.totalPassages > 0 then
                    let completed = update.completedPassages

                    if
                        completed = 0
                        || completed = update.totalPassages
                        || completed - lastReported >= 10
                    then
                        lastReported <- completed
                        report $"FsColbert indexed {completed}/{update.totalPassages} passage(s)."

            let! index =
                FsColbert.IndexBuilder.createFromPassagesWithCancellation
                    encoder
                    FsColbert.ChunkOptions.fsKameDefaults
                    passages
                    (Some progress)
                    cancellationToken

            throwIfCancellationRequested cancellationToken
            FsColbert.IndexPersistence.save path index
            return index
        }

    let InindexPassagesWithOptionsWithCancellation
        storageRoot
        report
        keywordOptions
        (pdfOptions: PdfIngestionOptions)
        source
        passages
        cancellationToken
        =
        async {
            try
                let pdfOptions = PdfIngestionOptions.sanitize pdfOptions
                throwIfCancellationRequested cancellationToken
                let! passages, keywordFingerprint = attachKeywords storageRoot report keywordOptions source passages
                throwIfCancellationRequested cancellationToken

                let path =
                    sourceIndexPathWithOptions storageRoot pdfOptions source keywordFingerprint

                match tryLoadPersistedIndex path with
                | Ok(Some _) ->
                    throwIfCancellationRequested cancellationToken
                    writeIndexMetadataWithOptions path pdfOptions source keywordFingerprint
                    return Ok()
                | Ok None
                | Error _ ->
                    report $"Preparing FsColbert model for {source.DisplayName}."
                    throwIfCancellationRequested cancellationToken
                    let! encoder = loadEncoder storageRoot
                    throwIfCancellationRequested cancellationToken
                    report $"Building FsColbert index for {source.DisplayName}."
                    let! _ = buildIndexFromChunks report encoder passages path cancellationToken
                    throwIfCancellationRequested cancellationToken
                    writeIndexMetadataWithOptions path pdfOptions source keywordFingerprint
                    return Ok()
            with
            | :? OperationCanceledException -> return raise (OperationCanceledException cancellationToken)
            | ex -> return Error $"Unable to build FsColbert index for {source.DisplayName}: {ex.Message}"
        }

    let InindexPassagesWithCancellation
        storageRoot
        report
        keywordOptions
        pdfParsingMode
        source
        passages
        cancellationToken
        =
        InindexPassagesWithOptionsWithCancellation
            storageRoot
            report
            keywordOptions
            (PdfIngestionOptions.create pdfParsingMode)
            source
            passages
            cancellationToken

    let InindexPassagesWithOptions storageRoot report keywordOptions pdfOptions source passages =
        InindexPassagesWithOptionsWithCancellation
            storageRoot
            report
            keywordOptions
            pdfOptions
            source
            passages
            CancellationToken.None

    let InindexPassages storageRoot report keywordOptions pdfParsingMode source passages =
        InindexPassagesWithCancellation
            storageRoot
            report
            keywordOptions
            pdfParsingMode
            source
            passages
            CancellationToken.None

    let InindexSourceWithOptions
        storageRoot
        report
        keywordOptions
        (pdfOptions: PdfIngestionOptions)
        (source: KnowledgeSource)
        =
        async {
            let pdfOptions = PdfIngestionOptions.sanitize pdfOptions

            let prebuilt = tryLoadPrebuiltIndex storageRoot source

            match prebuilt with
            | Ok(Some _) ->
                report $"Prebuilt FsColbert index is available for {source.DisplayName}."
                return Ok()
            | Error err -> return Error err
            | Ok None ->
                let! result = loadPassagesForIndexingWithOptions storageRoot report pdfOptions source

                match result with
                | Error err -> return Error err
                | Ok passages ->
                    return! InindexPassagesWithOptions storageRoot report keywordOptions pdfOptions source passages
        }

    let InindexSource storageRoot report keywordOptions pdfParsingMode source =
        InindexSourceWithOptions storageRoot report keywordOptions (PdfIngestionOptions.create pdfParsingMode) source

    let loadIndexWithOptions
        storageRoot
        report
        (keywordOptions: KeywordGenerationOptions)
        (pdfOptions: PdfIngestionOptions)
        buildMissingIndexes
        (sources: KnowledgeSource list)
        : Async<RetrievalIndex * string list> =
        async {
            let pdfOptions = PdfIngestionOptions.sanitize pdfOptions
            let sources = enabledSources sources

            if List.isEmpty sources then
                return { emptyIndex with sources = sources }, []
            else
                let! encoder = loadEncoder storageRoot
                let indices = ResizeArray<KnowledgeSource * FsColbert.ColbertIndex>()
                let errors = ResizeArray<string>()

                let keywordOptions =
                    if buildMissingIndexes then
                        keywordOptions
                    else
                        { keywordOptions with client = None }

                for source in sources do
                    let prebuilt = tryLoadPrebuiltIndex storageRoot source

                    match prebuilt with
                    | Ok(Some index) ->
                        report $"Loaded prebuilt FsColbert index for {source.DisplayName}."
                        indices.Add(source, index)
                    | Error err -> errors.Add err
                    | Ok None ->
                        if buildMissingIndexes then
                            let! result = loadPassagesForIndexingWithOptions storageRoot report pdfOptions source

                            match result with
                            | Error err -> errors.Add err
                            | Ok passages ->
                                let! passages, keywordFingerprint =
                                    attachKeywords storageRoot report keywordOptions source passages

                                let path =
                                    sourceIndexPathWithOptions storageRoot pdfOptions source keywordFingerprint

                                match tryLoadPersistedIndex path with
                                | Ok(Some index) ->
                                    writeIndexMetadataWithOptions path pdfOptions source keywordFingerprint
                                    indices.Add(source, index)
                                | Ok None ->
                                    report $"Building missing FsColbert index for {source.DisplayName}."

                                    let! index =
                                        buildIndexFromChunks report encoder passages path CancellationToken.None

                                    writeIndexMetadataWithOptions path pdfOptions source keywordFingerprint
                                    indices.Add(source, index)
                                | Error err -> errors.Add err
                        else
                            match loadPersistedIndexWithoutParsingWithOptions storageRoot report pdfOptions source with
                            | Ok(Some index) -> indices.Add(source, index)
                            | Ok None ->
                                match source.kind with
                                | Pdf ->
                                    errors.Add
                                        $"FsColbert index for {source.DisplayName} is missing for the selected {PdfParsingModes.displayName pdfOptions.parsingMode} PDF parser. Reprocess the source before connecting."
                                | Markdown
                                | Json ->
                                    errors.Add
                                        $"FsColbert index for {source.DisplayName} is missing. Reprocess the source before connecting."
                            | Error err -> errors.Add err

                let chunks =
                    indices |> Seq.collect (fun (s, idx) -> chunksFromIndex [ s ] idx) |> Seq.toList

                report $"Loaded FsColbert indices for {indices.Count} source(s)."

                return
                    { sources = sources
                      chunks = chunks
                      colbertIndices = List.ofSeq indices
                      encoder = Some encoder },
                    List.ofSeq errors
        }

    let loadIndex storageRoot report keywordOptions pdfParsingMode buildMissingIndexes sources =
        loadIndexWithOptions
            storageRoot
            report
            keywordOptions
            (PdfIngestionOptions.create pdfParsingMode)
            buildMissingIndexes
            sources

    let private renderedChunkMaxChars = 650

    let renderContextWithLimit maxContextChunks (chunks: SourceChunk list) =
        if List.isEmpty chunks then
            "No selected document context was available."
        else
            chunks
            |> List.truncate (max 1 maxContextChunks)
            |> List.mapi (fun index (chunk: SourceChunk) ->
                let body = Text.truncate renderedChunkMaxChars chunk.text
                $"[{index + 1}] {chunk.source.DisplayName} chunk {chunk.index}\n{body}")
            |> String.concat "\n\n"

    let renderContext chunks =
        renderContextWithLimit QaDefaults.maxContextChunks chunks

    let renderInventory (sources: KnowledgeSource list) =
        let enabled = sources |> List.filter _.enabled

        if List.isEmpty enabled then
            "No document sources are currently selected and ready."
        else
            enabled
            |> List.mapi (fun index source -> $"[{index + 1}] {source.DisplayName}")
            |> String.concat "\n"
