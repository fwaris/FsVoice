module FsVoice.Tests

open Xunit
open System
open System.IO
open System.Security.Cryptography
open System.Text
open System.Text.Json
open System.Threading
open System.Threading.Tasks
open System.Collections.Generic
open Microsoft.Extensions.AI
open FSharp.Control
open FsVoice.QA

type MockChatClient() =
    interface IChatClient with
        member this.Dispose() = ()
        member this.GetService(serviceType: Type, serviceKey: obj) = null

        member this.GetResponseAsync(chatMessages, options, cancellationToken) =
            let response =
                ChatResponse(
                    ChatMessage(
                        ChatRole.Assistant,
                        """{"terms":["synonym1","technicalTerm1"],"rewrittenQueries":["technical query"],"sectionName":null,"queryType":"Question"}"""
                    )
                )

            Task.FromResult(response)

        member this.GetStreamingResponseAsync(chatMessages, options, cancellationToken) =
            asyncSeq { yield ChatResponseUpdate() }

type InvalidJsonSchemaChatClient() =
    interface IChatClient with
        member this.Dispose() = ()
        member this.GetService(serviceType: Type, serviceKey: obj) = null

        member this.GetResponseAsync(chatMessages, options, cancellationToken) =
            InvalidOperationException("HTTP 400 invalid_request_error invalid_json_schema: schema type error")
            |> Task.FromException<ChatResponse>

        member this.GetStreamingResponseAsync(chatMessages, options, cancellationToken) =
            asyncSeq { yield ChatResponseUpdate() }

type SchemaFallbackChatClient(responseText: string, ?rejectJsonMode: bool) =
    let mutable strictAttempts = 0
    let mutable jsonModeAttempts = 0
    let mutable plainAttempts = 0

    member _.StrictAttempts = strictAttempts
    member _.JsonModeAttempts = jsonModeAttempts
    member _.PlainAttempts = plainAttempts

    interface IChatClient with
        member _.Dispose() = ()
        member _.GetService(serviceType: Type, serviceKey: obj) = null

        member _.GetResponseAsync(chatMessages, options, cancellationToken) =
            let hasJsonResponseFormat =
                not (isNull options)
                && match options.ResponseFormat with
                   | :? ChatResponseFormatJson -> true
                   | _ -> false

            let strict =
                if isNull options || isNull options.AdditionalProperties then
                    false
                else
                    let mutable value = Unchecked.defaultof<obj>
                    let hasStrict = options.AdditionalProperties.TryGetValue("strict", &value)

                    if hasStrict then
                        match value with
                        | :? bool as strict -> strict
                        | _ -> false
                    else
                        false

            if strict then
                strictAttempts <- strictAttempts + 1

                InvalidOperationException("HTTP 400 invalid_request_error invalid_json_schema: schema type error")
                |> Task.FromException<ChatResponse>
            elif defaultArg rejectJsonMode false && hasJsonResponseFormat then
                jsonModeAttempts <- jsonModeAttempts + 1

                InvalidOperationException(
                    "HTTP 400 invalid_request_error invalid_json_schema: response_format rejected"
                )
                |> Task.FromException<ChatResponse>
            else
                if hasJsonResponseFormat then
                    jsonModeAttempts <- jsonModeAttempts + 1
                else
                    plainAttempts <- plainAttempts + 1

                ChatResponse(ChatMessage(ChatRole.Assistant, responseText)) |> Task.FromResult

        member _.GetStreamingResponseAsync(chatMessages, options, cancellationToken) =
            asyncSeq { yield ChatResponseUpdate() }

type CountingChatClient(responseText: string) =
    let mutable count = 0

    member _.Count = count

    interface IChatClient with
        member _.Dispose() = ()
        member _.GetService(serviceType: Type, serviceKey: obj) = null

        member _.GetResponseAsync(chatMessages, options, cancellationToken) =
            count <- count + 1
            ChatResponse(ChatMessage(ChatRole.Assistant, responseText)) |> Task.FromResult

        member _.GetStreamingResponseAsync(chatMessages, options, cancellationToken) =
            asyncSeq { yield ChatResponseUpdate() }

type RecordingChatClient(responseText: string) =
    let mutable messages: ChatMessage list = []
    let mutable maxOutputTokens = Nullable<int>()
    let mutable temperature = Nullable<float32>()

    member _.Messages = messages
    member _.MaxOutputTokens = maxOutputTokens
    member _.Temperature = temperature

    interface IChatClient with
        member _.Dispose() = ()
        member _.GetService(serviceType: Type, serviceKey: obj) = null

        member _.GetResponseAsync(chatMessages, options, cancellationToken) =
            messages <- chatMessages |> Seq.toList

            if not (isNull options) then
                maxOutputTokens <- options.MaxOutputTokens
                temperature <- options.Temperature

            ChatResponse(ChatMessage(ChatRole.Assistant, responseText)) |> Task.FromResult

        member _.GetStreamingResponseAsync(chatMessages, options, cancellationToken) =
            asyncSeq { yield ChatResponseUpdate() }

type FakeQaToolHost() =
    interface IQaToolHost with
        member _.Report _ = ()

        member _.SearchKnowledgeAsync(_, _, _) = Task.FromResult("No source context.")

        member _.SourceInventoryAsync _ = Task.FromResult("No selected sources.")

        member _.SearchMemoryAsync(_, _, _) = Task.FromResult("No memory context.")

        member _.SearchBlackboardAsync(_, _) =
            Task.FromResult("No blackboard context.")

type FakeContextProvider(source: KnowledgeSource) =
    interface IQaContextProvider with
        member _.ProviderId = "fake.context"
        member _.DisplayName = "Fake Context"
        member _.Sources = [ source ]

        member _.LoadAsync _ = Task.FromResult([])

        member _.RetrieveAsync(request, _) =
            [ { source = source
                index = 0
                text = $"Fake context for {request.query}."
                score = 1.0f } ]
            |> Task.FromResult

        member _.InventoryAsync _ =
            Task.FromResult("Fake Context inventory.")

        member _.DisposeAsync() = ValueTask()

type FakeDoclingRasterizer(result: Result<FsColbert.DoclingRasterPage list, string>) =
    interface FsColbert.IDoclingPageRasterizer with
        member _.RasterizeAsync _ = async { return result }

type CountingDoclingOcr(cells: FsColbert.DoclingOcrCell list) =
    let mutable calls = 0

    member _.Calls = calls

    interface FsColbert.IDoclingOcrProvider with
        member _.RecognizeAsync _ =
            async {
                calls <- calls + 1
                return Ok cells
            }

type FakeDoclingLayout(predictions: FsColbert.DoclingLayoutPrediction list) =
    interface FsColbert.IDoclingLayoutPredictor with
        member _.PredictLayoutAsync pages =
            async {
                let requested = pages |> List.map _.pageNo |> Set.ofList

                return
                    predictions
                    |> List.filter (fun prediction -> Set.contains prediction.pageNo requested)
                    |> Ok
            }

type FailingDoclingLayout() =
    interface FsColbert.IDoclingLayoutPredictor with
        member _.PredictLayoutAsync _ =
            async { return Error "layout should not run" }

type FakeDoclingFigureClassifier(classes: FsColbert.DoclingFigureClass list) =
    interface FsColbert.IDoclingFigureClassifier with
        member _.ClassifyAsync _ = async { return Ok classes }

let private keywordPassage (source: KnowledgeSource) index text : FsColbert.PassageRef =
    { sourceId = source.location
      sourceDisplayName = source.DisplayName
      sourceLocation = source.location
      index = index
      text = text
      keywords = [] }

let private keywordOptions schemaVersion client =
    { KnowledgeSources.KeywordGenerationOptions.defaults with
        client = Some client
        schemaVersion = schemaVersion
        batchSize = 1
        parallelism = 2 }

let private tempStorageRoot () =
    Path.Combine(Path.GetTempPath(), Guid.NewGuid().ToString("N"))

let private hashText (value: string) =
    use sha = SHA256.Create()
    let bytes = Encoding.UTF8.GetBytes value

    sha.ComputeHash bytes
    |> Convert.ToHexString
    |> fun hash -> hash.ToLowerInvariant()

let private testSourceKindId sourceKind =
    match sourceKind with
    | Pdf -> "pdf"
    | Markdown -> "markdown"
    | Json -> "json"

let private testSourceFingerprint (source: KnowledgeSource) =
    let filePart =
        let info = FileInfo source.location

        if info.Exists then
            $"{info.FullName}:{info.Length}:{info.LastWriteTimeUtc.Ticks}"
        else
            source.location

    $"{testSourceKindId source.kind}:{filePart}"

let private doclingNativeCell text l bottom r top : FsColbert.DoclingOcrCell =
    { text = text
      bbox = FsColbert.DoclingGeometry.bottomLeftBox l bottom r top
      confidence = None }

let private doclingOcrCell text l t r b : FsColbert.DoclingOcrCell =
    { text = text
      bbox = FsColbert.DoclingGeometry.topLeftBox l t r b
      confidence = Some 0.9 }

let private doclingCluster id label l t r b : FsColbert.DoclingLayoutCluster =
    { id = id
      label = label
      confidence = 0.95f
      bbox = FsColbert.DoclingGeometry.topLeftBox l t r b
      cells = [] }

let private testKeywordCachePath storageRoot sourceFingerprint (options: KnowledgeSources.KeywordGenerationOptions) =
    let folder = Path.Combine(storageRoot, "FsVoice", "FsColbert", "KeywordCache")
    Directory.CreateDirectory folder |> ignore

    let fileName =
        [ yield sourceFingerprint
          yield options.modelId
          yield options.schemaVersion
          yield QaUseCaseProfile.fingerprint options.useCaseProfile

          if not (String.IsNullOrWhiteSpace options.useCaseFingerprint) then
              yield options.useCaseFingerprint ]
        |> String.concat "\n"
        |> hashText

    Path.Combine(folder, $"{fileName}.jsonl")

let private seedKeywordCache
    storageRoot
    (source: KnowledgeSource)
    (options: KnowledgeSources.KeywordGenerationOptions)
    (passage: FsColbert.PassageRef)
    keywords
    =
    let sourceFingerprint = testSourceFingerprint source
    let textHash = hashText passage.text

    let key =
        [ yield sourceFingerprint
          yield string passage.index
          yield textHash
          yield options.modelId
          yield options.schemaVersion
          yield QaUseCaseProfile.fingerprint options.useCaseProfile

          if not (String.IsNullOrWhiteSpace options.useCaseFingerprint) then
              yield options.useCaseFingerprint ]
        |> String.concat "\n"
        |> hashText

    let cachePath = testKeywordCachePath storageRoot sourceFingerprint options

    let line =
        JsonSerializer.Serialize(
            {| key = key
               sourceFingerprint = sourceFingerprint
               passageIndex = passage.index
               textHash = textHash
               modelId = options.modelId
               schemaVersion = options.schemaVersion
               profileFingerprint = QaUseCaseProfile.fingerprint options.useCaseProfile
               keywords = keywords |}
        )

    File.AppendAllText(cachePath, line + Environment.NewLine, Encoding.UTF8)

let private fakeEmbedding () : FsColbert.MultiVector =
    let dim = FsColbert.EncoderConfig.mxbaiEdgeColbert.embeddingDim
    let values = Array.zeroCreate<float32> dim
    values[0] <- 1.0f

    { tokenIds = [| 1 |]
      vectors = values
      tokenCount = 1
      embeddingDim = dim }

let private fakeIndexedPassage (source: KnowledgeSource) index text keywords : FsColbert.IndexedPassage =
    let reference: FsColbert.PassageRef =
        { sourceId = source.location
          sourceDisplayName = source.DisplayName
          sourceLocation = source.location
          index = index
          text = text
          keywords = keywords }

    { reference = reference
      embedding = fakeEmbedding ()
      terms = Set.empty }

let private fakeColbertIndex passages : FsColbert.ColbertIndex =
    { config = FsColbert.EncoderConfig.mxbaiEdgeColbert
      chunkOptions = FsColbert.ChunkOptions.fsKameDefaults
      tfidfOptions = FsColbert.TfidfOptions.defaults
      passages = passages
      tfidf = FsColbert.Tfidf.buildWithOptions FsColbert.TfidfOptions.defaults passages
      createdAt = DateTimeOffset.UtcNow }

let private prebuiltFolder storageRoot =
    let folder = Path.Combine(storageRoot, "FsVoice", "FsColbert", "Prebuilt")
    Directory.CreateDirectory folder |> ignore
    folder

[<Theory>]
[<InlineData("gpt-5.5")>]
[<InlineData("gpt-5.5-mini")>]
[<InlineData(" GPT-5.5 ")>]
let ``model capability omits temperature for models that reject it`` modelId =
    Assert.False(ModelCapabilities.supportsTemperature modelId)

[<Theory>]
[<InlineData("gpt-5.1")>]
[<InlineData("gpt-4.1-mini")>]
let ``model capability keeps temperature for supported models`` modelId =
    Assert.True(ModelCapabilities.supportsTemperature modelId)

[<Fact>]
let ``generic use case supplies model roles and runtime defaults`` () =
    let plugin = new GenericQaUseCasePlugin() :> IUseCasePlugin
    let definition = plugin.Definition |> UseCaseDefinition.sanitize

    Assert.Equal(UseCaseDefinition.currentContractVersion, plugin.ContractVersion)
    Assert.Equal("generic", definition.id)
    Assert.Equal("gpt-realtime-2", (UseCaseDefinition.model Realtime definition).modelId)
    Assert.Equal("gpt-5.5", (UseCaseDefinition.model Answer definition).modelId)
    Assert.Equal("gpt-5-nano", (UseCaseDefinition.model Keyword definition).modelId)
    Assert.True(definition.runtime.enableToolPlanner)
    Assert.False(definition.runtime.enableQueryExpansion)

[<Fact>]
let ``qa session applies custom prompts and answer role options`` () =
    task {
        let source =
            { kind = Json
              location = "memory://fake"
              enabled = true }

        let recorder = new RecordingChatClient("custom response")

        let answerConfig =
            { ModelRoleConfig.create "gpt-4.1-mini" with
                maxOutputTokens = Some 123
                temperature = Some 0.1f }

        let options =
            { QaSessionOptions.create (tempStorageRoot ()) with
                autoWriteback = false
                contextProviders = [ FakeContextProvider(source) ]
                clients =
                    { QaModelClients.none with
                        answerGenerator = Some recorder }
                answerModelId = answerConfig.modelId
                modelRoles = UseCaseDefinition.defaultModels |> Map.add Answer answerConfig
                prompts =
                    { PromptSet.empty with
                        answerSystem = Some "CUSTOM SYSTEM"
                        answerUserTemplate = Some "Q={{question}}\nCTX={{sourceContext}}\nINV={{sourceInventory}}" } }

        use session = new QaSession(options)

        let request =
            { turnId = Guid.NewGuid().ToString("N")
              question = "What does the fake context say?"
              realtimeJudgement = None
              deadline = None }

        let! answer = session.AnswerAsync(request, CancellationToken.None)

        Assert.Equal("custom response", answer.answer)
        Assert.Equal(123, recorder.MaxOutputTokens.Value)
        Assert.Equal(0.1f, recorder.Temperature.Value)
        Assert.Equal("CUSTOM SYSTEM", recorder.Messages[0].Text)
        Assert.Contains("Q=What does the fake context say?", recorder.Messages[1].Text)
        Assert.Contains("Fake context for What does the fake context say?", recorder.Messages[1].Text)
    }

[<Fact>]
let ``getSynonyms parses comma-separated keywords correctly`` () =
    async {
        let client = new MockChatClient()
        let mutable reportedMsg = ""
        let report msg = reportedMsg <- msg
        let! expansion = KnowledgeSources.getSynonyms client (Some report) "query"

        match expansion with
        | Some expansion ->
            Assert.Equal<int>(2, expansion.terms.Length)
            Assert.Contains("synonym1", expansion.terms)
            Assert.Contains("technicalTerm1", expansion.terms)
            Assert.Equal<KnowledgeSources.QueryType>(KnowledgeSources.QueryType.Question, expansion.queryType)
            Assert.Contains("Retrieval query", reportedMsg)
        | None -> failwith "Expected query expansion."
    }
    |> Async.RunSynchronously

[<Fact>]
let ``getSynonyms handles empty query`` () =
    async {
        let client = new MockChatClient()
        let! expansion = KnowledgeSources.getSynonyms client None ""
        Assert.True(Option.isNone expansion)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``query post-processing keeps domain expansion in supplied use-case profile`` () =
    let profile =
        { QaUseCaseProfile.generic with
            id = "test-domain"
            displayName = "Test Domain"
            voiceReplacements =
                [ { pattern = @"\bmedicare\s+gap\b"
                    replacement = "Medigap" } ]
            queryExpansionRules =
                [ { triggers = [ "medigap" ]
                    terms = [ "medicare"; "supplement"; "plan" ] } ] }

    let generic = Text.QueryPostProcessing.forVoiceLikeRetrieval "medicare gap"

    let profiled =
        Text.QueryPostProcessing.forVoiceLikeRetrievalWithProfile profile "medicare gap"

    Assert.DoesNotContain("supplement", generic.searchTerms)
    Assert.Contains("supplement", profiled.searchTerms)
    Assert.Contains("Medigap", profiled.normalizedQuery)

[<Fact>]
let ``json knowledge source can be loaded as generic QA content`` () =
    async {
        let path =
            Path.Combine(Path.GetTempPath(), $"fsvoice-json-source-{Guid.NewGuid():N}.json")

        File.WriteAllText(
            path,
            JsonSerializer.Serialize(
                {| id = "test-source"
                   title = "Test Source"
                   documents =
                    [ {| id = "1"
                         title = "Answer label: 1"
                         text = "Use the scheduling guide for emergency dental appointments."
                         keywords = [ "appointment lookup" ] |} ] |}
            )
        )

        let source =
            { kind = Json
              location = path
              enabled = true }

        let! retrieval, errors = KnowledgeSources.loadInternalIndex [ source ]

        Assert.Empty errors
        let chunk = Assert.Single retrieval.chunks
        Assert.Contains("Answer label: 1", chunk.text)
        Assert.Contains("emergency dental appointments", chunk.text)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling json knowledge source loads passages with keywords`` () =
    async {
        let path =
            Path.Combine(Path.GetTempPath(), $"fsvoice-docling-source-{Guid.NewGuid():N}.json")

        let page: FsColbert.DoclingPageItem =
            { pageNo = 1
              size = { width = 100.0; height = 100.0 } }

        let item: FsColbert.DoclingTextItem =
            { selfRef = "#/texts/0"
              parent = "#/body"
              label = FsColbert.DoclingLabels.ofJsonValue "text"
              text = "Orthodontia benefits require a waiting period."
              orig = "Orthodontia benefits require a waiting period."
              contentLayer = FsColbert.DoclingContentLayer.Body
              prov = []
              keywords = [ "dental braces"; "orthodontia" ]
              sourceId = Some "benefits"
              sourceDisplayName = Some "Benefits" }

        let document: FsColbert.DoclingDocument =
            { name = "benefits"
              originFileName = Some "benefits.pdf"
              originMimeType = Some "application/pdf"
              pages = [ 1, page ] |> Map.ofList
              texts = [ item ]
              tables = []
              pictures = []
              bodyChildren = [ "#/texts/0" ]
              furnitureChildren = [] }

        File.WriteAllText(path, FsColbert.DoclingJson.serialize document)

        let source =
            { kind = Json
              location = path
              enabled = true }

        let! passages = KnowledgeSources.loadPassages source

        match passages with
        | Error err -> failwith err
        | Ok passages ->
            let passage = Assert.Single passages
            Assert.Contains("Orthodontia benefits", passage.text)
            Assert.Equal<string list>([ "dental braces"; "orthodontia" ], passage.keywords)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid page input builder skips ocr when native pdf text is sufficient`` () =
    async {
        let image = FsColbert.DoclingRgbImage.solid 400 400 255uy 255uy 255uy
        let rasterizer = FakeDoclingRasterizer(Ok [ { pageNo = 1; image = image } ])

        let nativeProvider _ =
            async {
                let nativePage: FsColbert.DoclingNativePageText =
                    { pageNo = 1
                      size = { width = 200.0; height = 200.0 }
                      cells =
                        [ doclingNativeCell "Native" 10.0 145.0 60.0 160.0
                          doclingNativeCell "PDF" 65.0 145.0 95.0 160.0
                          doclingNativeCell "text" 100.0 145.0 135.0 160.0 ] }

                return Ok [ nativePage ]
            }

        let ocr = CountingDoclingOcr [ doclingOcrCell "OCR fallback" 20.0 80.0 120.0 100.0 ]

        let! result =
            DoclingHybrid.buildPageInputs
                { DoclingHybrid.defaults with
                    minNativeCharsPerPage = 8 }
                ignore
                "/tmp/native.pdf"
                rasterizer
                nativeProvider
                (Some(ocr :> FsColbert.IDoclingOcrProvider))

        match result with
        | Error err -> failwith err
        | Ok inputs ->
            let input = Assert.Single inputs
            Assert.Equal(0, ocr.Calls)
            Assert.Equal<string list>([ "Native"; "PDF"; "text" ], input.ocrCells |> List.map _.text)
            Assert.Equal(FsColbert.DoclingCoordinateOrigin.BottomLeft, input.ocrCells.Head.bbox.coordOrigin)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid built-in layout providers resolve pp and heron`` () =
    let pp = DoclingHybrid.tryBuiltInLayoutProvider "pp-doclayout-m"
    let heron = DoclingHybrid.tryBuiltInLayoutProvider "heron"

    Assert.True(pp.IsSome)
    Assert.Equal("pp-doclayout-m", pp.Value.Id)
    Assert.True(heron.IsSome)
    Assert.Equal("heron", heron.Value.Id)

[<Fact>]
let ``pdf parser index fingerprint includes layout quality revision`` () =
    let hybrid =
        KnowledgeSources.PdfParsingModes.indexFingerprint KnowledgeSources.PdfParsingMode.Hybrid

    let withoutLayout =
        KnowledgeSources.PdfParsingModes.indexFingerprint KnowledgeSources.PdfParsingMode.HybridWithoutLayout

    Assert.Contains("pdfParsingMode=hybrid", hybrid)
    Assert.Contains("pdfParserQuality=layout-sparse-fallback-v1", hybrid)
    Assert.False(String.Equals(hybrid, withoutLayout, StringComparison.Ordinal))

[<Fact>]
let ``docling hybrid layout disabled bypasses supplied layout predictor`` () =
    async {
        let image = FsColbert.DoclingRgbImage.solid 400 400 255uy 255uy 255uy
        let rasterizer = FakeDoclingRasterizer(Ok [ { pageNo = 1; image = image } ])

        let nativeProvider _ =
            async {
                let nativePage: FsColbert.DoclingNativePageText =
                    { pageNo = 1
                      size = { width = 200.0; height = 200.0 }
                      cells = [ doclingNativeCell "Native layout bypass text" 10.0 145.0 180.0 160.0 ] }

                return Ok [ nativePage ]
            }

        let source =
            FsColbert.PassageSource.create "/tmp/no-layout.pdf" "No Layout" "/tmp/no-layout.pdf"

        let! result =
            DoclingHybrid.readPdfPassagesWithProvidersAndCancellation
                { DoclingHybrid.defaults with
                    enableLayoutAnalysis = false
                    minNativeCharsPerPage = 8 }
                ignore
                FsColbert.ChunkOptions.fsKameDefaults
                source
                "/tmp/no-layout.pdf"
                rasterizer
                nativeProvider
                None
                (FailingDoclingLayout() :> FsColbert.IDoclingLayoutPredictor)
                None
                CancellationToken.None

        match result with
        | Error err -> failwith err
        | Ok passages -> Assert.NotEmpty passages
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid provider path emits native text table picture metadata and keywords`` () =
    async {
        let image = FsColbert.DoclingRgbImage.solid 400 400 255uy 255uy 255uy
        let rasterizer = FakeDoclingRasterizer(Ok [ { pageNo = 1; image = image } ])

        let nativeProvider _ =
            async {
                let nativePage: FsColbert.DoclingNativePageText =
                    { pageNo = 1
                      size = { width = 400.0; height = 400.0 }
                      cells =
                        [ doclingNativeCell "Native paragraph" 20.0 285.0 180.0 310.0
                          doclingNativeCell "Table row" 30.0 155.0 120.0 175.0 ] }

                return Ok [ nativePage ]
            }

        let layout =
            FakeDoclingLayout
                [ { pageNo = 1
                    clusters =
                      [ doclingCluster 0 FsColbert.DoclingLabel.Text 15.0 80.0 220.0 130.0
                        doclingCluster 1 FsColbert.DoclingLabel.Table 20.0 210.0 160.0 260.0
                        doclingCluster 2 FsColbert.DoclingLabel.Picture 250.0 190.0 360.0 300.0 ] } ]
            :> FsColbert.IDoclingLayoutPredictor

        let classifier =
            let figureClass: FsColbert.DoclingFigureClass =
                { className = "diagram"
                  confidence = 0.91f }

            FakeDoclingFigureClassifier [ figureClass ] :> FsColbert.IDoclingFigureClassifier

        let passageSource =
            FsColbert.PassageSource.create "/tmp/doc.pdf" "Doc" "/tmp/doc.pdf"

        let! result =
            DoclingHybrid.readPdfPassagesWithProviders
                { DoclingHybrid.defaults with
                    minNativeCharsPerPage = 8 }
                ignore
                FsColbert.ChunkOptions.fsKameDefaults
                passageSource
                "/tmp/doc.pdf"
                rasterizer
                nativeProvider
                None
                layout
                (Some classifier)

        match result with
        | Error err -> failwith err
        | Ok passages ->
            let passage = Assert.Single passages
            Assert.Contains("Native paragraph", passage.text)
            Assert.Contains("Table row", passage.text)
            Assert.Contains("[Picture: diagram", passage.text)
            Assert.Contains("table", passage.keywords)
            Assert.Contains("picture", passage.keywords)
            Assert.Contains("diagram", passage.keywords)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid provider path falls back to native text when layout collapses`` () =
    async {
        let image = FsColbert.DoclingRgbImage.solid 400 400 255uy 255uy 255uy

        let rasterizer =
            FakeDoclingRasterizer(Ok [ { pageNo = 1; image = image }; { pageNo = 2; image = image } ])

        let repeatedText prefix =
            [ 1..170 ] |> List.map (fun index -> $"{prefix}{index}") |> String.concat " "

        let nativeProvider _ =
            async {
                let pages: FsColbert.DoclingNativePageText list =
                    [ { pageNo = 1
                        size = { width = 400.0; height = 400.0 }
                        cells = [ doclingNativeCell (repeatedText "alpha") 20.0 350.0 380.0 380.0 ] }
                      { pageNo = 2
                        size = { width = 400.0; height = 400.0 }
                        cells = [ doclingNativeCell (repeatedText "beta") 20.0 350.0 380.0 380.0 ] } ]

                return Ok pages
            }

        let layout =
            FakeDoclingLayout
                [ { pageNo = 1
                    clusters = [ doclingCluster 0 FsColbert.DoclingLabel.Text 15.0 15.0 390.0 70.0 ] }
                  { pageNo = 2; clusters = [] } ]
            :> FsColbert.IDoclingLayoutPredictor

        let passageSource =
            FsColbert.PassageSource.create "/tmp/collapsed.pdf" "Collapsed" "/tmp/collapsed.pdf"

        let reports = ResizeArray<string>()

        let! result =
            DoclingHybrid.readPdfPassagesWithProviders
                { DoclingHybrid.defaults with
                    minNativeCharsPerPage = 8 }
                reports.Add
                FsColbert.ChunkOptions.fsKameDefaults
                passageSource
                "/tmp/collapsed.pdf"
                rasterizer
                nativeProvider
                None
                layout
                None

        match result with
        | Error err -> failwith err
        | Ok passages ->
            Assert.True(passages.Length > 1)
            Assert.Contains(passages, fun passage -> passage.text.Contains("beta"))
            Assert.Contains(reports, fun message -> message.Contains("layout conversion produced only 1 passage"))
            Assert.Contains(reports, fun message -> message.Contains("Using Docling native-text conversion"))
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid provider path falls back to native text when layout is sparse`` () =
    async {
        let image = FsColbert.DoclingRgbImage.solid 400 400 255uy 255uy 255uy

        let rasterizer =
            FakeDoclingRasterizer(
                Ok
                    [ { pageNo = 1; image = image }
                      { pageNo = 2; image = image }
                      { pageNo = 3; image = image }
                      { pageNo = 4; image = image } ]
            )

        let repeatedText prefix =
            [ 1..220 ] |> List.map (fun index -> $"{prefix}{index}") |> String.concat " "

        let nativeProvider _ =
            async {
                let pageText pageNo prefix : FsColbert.DoclingNativePageText =
                    { pageNo = pageNo
                      size = { width = 400.0; height = 400.0 }
                      cells = [ doclingNativeCell (repeatedText prefix) 20.0 350.0 380.0 380.0 ] }

                return
                    [ pageText 1 "alpha"
                      pageText 2 "beta"
                      pageText 3 "gamma"
                      pageText 4 "delta" ]
                    |> Ok
            }

        let layout =
            FakeDoclingLayout
                [ { pageNo = 1
                    clusters = [ doclingCluster 0 FsColbert.DoclingLabel.Text 15.0 15.0 390.0 70.0 ] }
                  { pageNo = 2
                    clusters = [ doclingCluster 1 FsColbert.DoclingLabel.Text 15.0 15.0 390.0 70.0 ] }
                  { pageNo = 3; clusters = [] }
                  { pageNo = 4; clusters = [] } ]
            :> FsColbert.IDoclingLayoutPredictor

        let passageSource =
            FsColbert.PassageSource.create "/tmp/sparse.pdf" "Sparse" "/tmp/sparse.pdf"

        let reports = ResizeArray<string>()

        let! result =
            DoclingHybrid.readPdfPassagesWithProviders
                { DoclingHybrid.defaults with
                    minNativeCharsPerPage = 8 }
                reports.Add
                FsColbert.ChunkOptions.fsKameDefaults
                passageSource
                "/tmp/sparse.pdf"
                rasterizer
                nativeProvider
                None
                layout
                None

        match result with
        | Error err -> failwith err
        | Ok passages ->
            Assert.True(passages.Length > 2)
            Assert.Contains(passages, fun passage -> passage.text.Contains("gamma"))
            Assert.Contains(passages, fun passage -> passage.text.Contains("delta"))
            Assert.Contains(reports, fun message -> message.Contains("layout conversion looked incomplete"))
            Assert.Contains(reports, fun message -> message.Contains("Using Docling native-text conversion"))
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid cancellation does not fall back to legacy parser`` () =
    let storageRoot =
        Path.Combine(Path.GetTempPath(), $"fsvoice-docling-cancel-{Guid.NewGuid():N}")

    Directory.CreateDirectory storageRoot |> ignore

    try
        let passageSource =
            FsColbert.PassageSource.create "/tmp/cancel.pdf" "Cancel" "/tmp/cancel.pdf"

        let mutable legacyCalled = false

        use cts = new CancellationTokenSource()
        cts.Cancel()

        Assert.Throws<OperationCanceledException>(fun () ->
            DoclingHybrid.readPdfPassagesWithFallbackAndCancellation
                storageRoot
                ignore
                FsColbert.ChunkOptions.fsKameDefaults
                passageSource
                "/tmp/cancel.pdf"
                (fun () ->
                    async {
                        legacyCalled <- true
                        return Ok []
                    })
                cts.Token
            |> Async.RunSynchronously
            |> ignore)
        |> ignore

        Assert.False legacyCalled
    finally
        if Directory.Exists storageRoot then
            Directory.Delete(storageRoot, true)

[<Fact>]
let ``indexing cancellation is rethrown instead of wrapped as an error`` () =
    let storageRoot =
        Path.Combine(Path.GetTempPath(), $"fsvoice-index-cancel-{Guid.NewGuid():N}")

    Directory.CreateDirectory storageRoot |> ignore

    try
        let source: KnowledgeSource =
            { kind = Pdf
              location = "/tmp/cancel.pdf"
              enabled = true }

        let passage: FsColbert.PassageRef =
            { sourceId = source.location
              sourceDisplayName = source.DisplayName
              sourceLocation = source.location
              index = 0
              text = "cancel me"
              keywords = [] }

        use cts = new CancellationTokenSource()
        cts.Cancel()

        Assert.Throws<OperationCanceledException>(fun () ->
            KnowledgeSources.InindexPassagesWithCancellation
                storageRoot
                ignore
                KnowledgeSources.KeywordGenerationOptions.disabled
                KnowledgeSources.PdfParsingMode.Legacy
                source
                [ passage ]
                cts.Token
            |> Async.RunSynchronously
            |> ignore)
        |> ignore
    finally
        if Directory.Exists storageRoot then
            Directory.Delete(storageRoot, true)

[<Fact>]
let ``docling hybrid fallback uses legacy pdf reader when provider path fails`` () =
    async {
        let source =
            FsColbert.PassageSource.create "fallback" "Fallback" "/tmp/fallback.pdf"

        let reports = ResizeArray<string>()

        let legacyReader () =
            async {
                let passage: FsColbert.PassageRef =
                    { sourceId = source.id
                      sourceDisplayName = source.displayName
                      sourceLocation = source.location
                      index = 0
                      text = "Legacy PdfPig text"
                      keywords = [] }

                return Ok [ passage ]
            }

        let! result =
            DoclingHybrid.fallbackToLegacy
                reports.Add
                source.location
                (async { return Error "layout model missing" })
                legacyReader

        match result with
        | Error err -> failwith err
        | Ok passages ->
            let passage = Assert.Single passages
            Assert.Equal("Legacy PdfPig text", passage.text)
            Assert.Contains(reports, fun message -> message.Contains("Falling back to PdfPig text extraction"))
    }
    |> Async.RunSynchronously

[<Fact>]
let ``docling hybrid fallback uses legacy pdf reader when hybrid returns one passage`` () =
    async {
        let source =
            FsColbert.PassageSource.create "fallback" "Fallback" "/tmp/fallback.pdf"

        let reports = ResizeArray<string>()

        let passage index text : FsColbert.PassageRef =
            { sourceId = source.id
              sourceDisplayName = source.displayName
              sourceLocation = source.location
              index = index
              text = text
              keywords = [] }

        let legacyReader () =
            async { return Ok [ passage 0 "Legacy PdfPig text 1"; passage 1 "Legacy PdfPig text 2" ] }

        let! result =
            DoclingHybrid.fallbackToLegacy
                reports.Add
                source.location
                (async { return Ok [ passage 0 "Docling single blob" ] })
                legacyReader

        match result with
        | Error err -> failwith err
        | Ok passages ->
            Assert.Equal(2, passages.Length)
            Assert.Equal("Legacy PdfPig text 1", passages.Head.text)
            Assert.Contains(reports, fun message -> message.Contains("produced only 1 passage"))
            Assert.Contains(reports, fun message -> message.Contains("Using PdfPig fallback"))
    }
    |> Async.RunSynchronously

[<Fact>]
let ``legacy prebuilt manifest still loads persisted index keywords`` () =
    let storageRoot = tempStorageRoot ()
    let sourcePath = Path.Combine(storageRoot, "legacy.md")
    Directory.CreateDirectory storageRoot |> ignore
    File.WriteAllText(sourcePath, "Legacy markdown source.")

    let source =
        { kind = Markdown
          location = sourcePath
          enabled = true }

    let folder = prebuiltFolder storageRoot
    let indexPath = Path.Combine(folder, "legacy.fsci")

    fakeColbertIndex [ fakeIndexedPassage source 0 "Policy waiting period text." [ "orthodontia" ] ]
    |> FsColbert.IndexPersistence.save indexPath

    let manifest =
        [| {| id = "legacy"
              kind = "markdown"
              displayName = "Legacy"
              storedPath = sourcePath
              indexPath = indexPath |} |]

    File.WriteAllText(Path.Combine(folder, "prebuilt-indexes.installed.json"), JsonSerializer.Serialize manifest)

    match KnowledgeSources.tryLoadPrebuiltIndex storageRoot source with
    | Error err -> failwith err
    | Ok None -> failwith "Expected prebuilt index."
    | Ok(Some index) ->
        Assert.Equal(1, index.passages.Length)
        Assert.Equal<string list>([ "orthodontia" ], index.passages.Head.reference.keywords)

[<Fact>]
let ``bundle manifest loads prebuilt index and keeps keyword tfidf ranking`` () =
    let storageRoot = tempStorageRoot ()
    let sourcePath = Path.Combine(storageRoot, "docling.json")
    Directory.CreateDirectory storageRoot |> ignore
    File.WriteAllText(sourcePath, "{}")

    let source =
        { kind = Json
          location = sourcePath
          enabled = true }

    let folder = prebuiltFolder storageRoot
    let indexPath = Path.Combine(folder, "docling.fsci")

    fakeColbertIndex
        [ fakeIndexedPassage source 0 "Benefits waiting period details." [ "orthodontia"; "dental braces" ]
          fakeIndexedPassage source 1 "Kitchen recipe notes." [] ]
    |> FsColbert.IndexPersistence.save indexPath

    let bundleSource: FsColbert.IndexBundleSource =
        { sourceId = sourcePath
          sourceDisplayName = "Docling"
          sourceLocation = Some sourcePath
          sourceKind = Some "docling-json"
          indexFile = "docling.fsci" }

    FsColbert.IndexBundle.create
        "test-bundle"
        "1.0.0"
        FsColbert.ModelCatalog.mxbaiEdgeColbertInt8.id
        FsColbert.ChunkOptions.fsKameDefaults
        FsColbert.TfidfOptions.defaults
        [ bundleSource ]
    |> FsColbert.IndexBundle.writeManifest (Path.Combine(folder, "index-bundle.json"))

    match KnowledgeSources.tryLoadPrebuiltIndex storageRoot source with
    | Error err -> failwith err
    | Ok None -> failwith "Expected bundled index."
    | Ok(Some index) ->
        let keywordCount =
            index.passages |> List.sumBy (fun passage -> passage.reference.keywords.Length)

        let candidates =
            FsColbert.Tfidf.scoreQuery index.tfidf "orthodontia"
            |> FsColbert.Tfidf.topCandidates 10

        Assert.True(keywordCount > 0)
        Assert.Equal(0, candidates[0] |> fst)

[<Fact>]
let ``bundle manifest mismatch reports compatibility reason`` () =
    let storageRoot = tempStorageRoot ()
    let sourcePath = Path.Combine(storageRoot, "docling.json")
    Directory.CreateDirectory storageRoot |> ignore
    File.WriteAllText(sourcePath, "{}")

    let source =
        { kind = Json
          location = sourcePath
          enabled = true }

    let folder = prebuiltFolder storageRoot
    let indexPath = Path.Combine(folder, "docling.fsci")

    fakeColbertIndex [ fakeIndexedPassage source 0 "Benefits waiting period details." [ "orthodontia" ] ]
    |> FsColbert.IndexPersistence.save indexPath

    FsColbert.IndexBundle.create
        "test-bundle"
        "1.0.0"
        "wrong/model"
        FsColbert.ChunkOptions.fsKameDefaults
        FsColbert.TfidfOptions.defaults
        [ { sourceId = sourcePath
            sourceDisplayName = "Docling"
            sourceLocation = Some sourcePath
            sourceKind = Some "docling-json"
            indexFile = "docling.fsci" } ]
    |> FsColbert.IndexBundle.writeManifest (Path.Combine(folder, "index-bundle.json"))

    match KnowledgeSources.tryLoadPrebuiltIndex storageRoot source with
    | Ok _ -> failwith "Expected incompatible bundle."
    | Error err -> Assert.Contains("model_id", err)

[<Fact>]
let ``qa session composes injected context providers`` () =
    task {
        let source =
            { kind = Json
              location = "fake://source"
              enabled = true }

        let provider = FakeContextProvider source :> IQaContextProvider
        let storageRoot = tempStorageRoot ()

        let options =
            { QaSessionOptions.create storageRoot with
                autoWriteback = false }

        let session = new QaSession(options)
        let! errors = (session :> IQaOrchestrator).ConfigureAsync([ provider ], CancellationToken.None)

        let request =
            { turnId = Guid.NewGuid().ToString("N")
              question = "composition"
              realtimeJudgement = None
              deadline = None }

        let! answer = session.AnswerAsync(request, CancellationToken.None)

        Assert.Empty errors
        Assert.Single answer.context |> ignore
        Assert.Contains("composition", answer.context.Head.text)
        Assert.Equal<KnowledgeSource list>([ source ], answer.inventory)

        do! (session :> IAsyncDisposable).DisposeAsync().AsTask()
    }

[<Fact>]
let ``qa session skips llm tool planner when only built-in context tools are available`` () =
    task {
        let source =
            { kind = Json
              location = "fake://source"
              enabled = true }

        let planner = new CountingChatClient("""{"calls":[]}""")
        let provider = FakeContextProvider source :> IQaContextProvider
        let storageRoot = tempStorageRoot ()

        let options =
            { QaSessionOptions.create storageRoot with
                autoWriteback = false
                clients =
                    { QaModelClients.none with
                        toolPlanner = Some planner } }

        let session = new QaSession(options)
        let! _ = (session :> IQaOrchestrator).ConfigureAsync([ provider ], CancellationToken.None)

        let request =
            { turnId = Guid.NewGuid().ToString("N")
              question = "composition"
              realtimeJudgement = None
              deadline = None }

        let! _ = session.AnswerAsync(request, CancellationToken.None)

        Assert.Equal(0, planner.Count)

        do! (session :> IAsyncDisposable).DisposeAsync().AsTask()
    }

[<Fact>]
let ``qa session does not call llm query expansion by default`` () =
    task {
        let path =
            Path.Combine(Path.GetTempPath(), $"fsvoice-no-query-expansion-{Guid.NewGuid():N}.json")

        File.WriteAllText(
            path,
            JsonSerializer.Serialize(
                {| documents =
                    [ {| id = "1"
                         title = "Policy"
                         text = "The debug answer is BLUE-42."
                         keywords = [] |} ] |}
            )
        )

        let expansion =
            new CountingChatClient(
                """{"terms":["BLUE-42"],"rewrittenQueries":["BLUE-42"],"sectionName":null,"queryType":"Question"}"""
            )

        let options =
            { QaSessionOptions.create (tempStorageRoot ()) with
                autoWriteback = false
                clients =
                    { QaModelClients.none with
                        queryExpansion = Some expansion } }

        use session = new QaSession(options)

        let source =
            { kind = Json
              location = path
              enabled = true }

        let! _ = session.LoadSourcesAsync(InternalDocumentIndex, [ source ], CancellationToken.None)

        let request =
            { turnId = Guid.NewGuid().ToString("N")
              question = "What is the debug answer?"
              realtimeJudgement = None
              deadline = None }

        let! answer = session.AnswerAsync(request, CancellationToken.None)

        Assert.Equal(0, expansion.Count)
        Assert.Contains("BLUE-42", answer.context.Head.text)
    }

[<Fact>]
let ``keyword schema rejection does not block keyword attachment`` () =
    async {
        let source =
            { kind = Markdown
              location = $"/tmp/schema-reject-{Guid.NewGuid():N}.md"
              enabled = true }

        let passages =
            [ keywordPassage source 0 "The guide covers emergency dental scheduling."
              keywordPassage source 1 "The guide excludes cosmetic dental scheduling." ]

        let logs = ResizeArray<string>()
        let storageRoot = tempStorageRoot ()

        let! enriched, _ =
            KnowledgeSources.attachKeywords
                storageRoot
                logs.Add
                (keywordOptions $"test-schema-{Guid.NewGuid():N}" (new InvalidJsonSchemaChatClient() :> IChatClient))
                source
                passages

        Assert.Equal<int>(2, enriched.Length)
        Assert.All(enriched, fun passage -> Assert.Empty passage.keywords)

        let schemaRejectionLogs =
            logs
            |> Seq.filter (fun log -> log.Contains("OpenAI rejected keyword schema and response-format fallbacks"))
            |> Seq.length

        Assert.Equal(1, schemaRejectionLogs)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``keyword schema rejection retries with json mode`` () =
    async {
        let source =
            { kind = Markdown
              location = $"/tmp/schema-fallback-{Guid.NewGuid():N}.md"
              enabled = true }

        let passages =
            [ keywordPassage source 0 "The guide covers emergency dental scheduling."
              keywordPassage source 1 "The guide excludes cosmetic dental scheduling." ]

        let client =
            new SchemaFallbackChatClient(
                """{"items":[{"passageIndex":0,"keywords":["emergency dental scheduling"]},{"passageIndex":1,"keywords":["cosmetic dental exclusion"]}]}"""
            )

        let logs = ResizeArray<string>()
        let storageRoot = tempStorageRoot ()

        let! enriched, _ =
            KnowledgeSources.attachKeywords
                storageRoot
                logs.Add
                (keywordOptions $"test-schema-fallback-{Guid.NewGuid():N}" (client :> IChatClient))
                source
                passages

        Assert.Equal<string list>([ "emergency dental scheduling" ], enriched.Head.keywords)
        Assert.Equal<string list>([ "cosmetic dental exclusion" ], enriched.Tail.Head.keywords)
        Assert.True(client.StrictAttempts > 0)
        Assert.True(client.JsonModeAttempts > 0)

        Assert.True(
            logs
            |> Seq.exists (fun log -> log.Contains("retrying keyword generation in JSON mode"))
        )

        let finalSchemaRejectionLogs =
            logs
            |> Seq.filter (fun log -> log.Contains("indexing will continue without generated keywords"))
            |> Seq.length

        Assert.Equal(0, finalSchemaRejectionLogs)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``keyword response format rejection retries with plain json`` () =
    async {
        let source =
            { kind = Markdown
              location = $"/tmp/format-fallback-{Guid.NewGuid():N}.md"
              enabled = true }

        let passages =
            [ keywordPassage source 0 "The guide covers emergency dental scheduling."
              keywordPassage source 1 "The guide excludes cosmetic dental scheduling." ]

        let client =
            new SchemaFallbackChatClient(
                """{"items":[{"passageIndex":0,"keywords":["emergency dental scheduling"]},{"passageIndex":1,"keywords":["cosmetic dental exclusion"]}]}""",
                rejectJsonMode = true
            )

        let logs = ResizeArray<string>()
        let storageRoot = tempStorageRoot ()

        let! enriched, _ =
            KnowledgeSources.attachKeywords
                storageRoot
                logs.Add
                (keywordOptions $"test-format-fallback-{Guid.NewGuid():N}" (client :> IChatClient))
                source
                passages

        Assert.Equal<string list>([ "emergency dental scheduling" ], enriched.Head.keywords)
        Assert.Equal<string list>([ "cosmetic dental exclusion" ], enriched.Tail.Head.keywords)
        Assert.True(client.StrictAttempts > 0)
        Assert.True(client.JsonModeAttempts > 0)
        Assert.True(client.PlainAttempts > 0)

        Assert.True(
            logs
            |> Seq.exists (fun log -> log.Contains("retrying keyword generation without response formatting"))
        )

        let finalSchemaRejectionLogs =
            logs
            |> Seq.filter (fun log -> log.Contains("indexing will continue without generated keywords"))
            |> Seq.length

        Assert.Equal(0, finalSchemaRejectionLogs)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``keyword schema rejection keeps cached keyword records`` () =
    async {
        let schemaVersion = $"test-cache-schema-{Guid.NewGuid():N}"
        let storageRoot = tempStorageRoot ()

        let source =
            { kind = Markdown
              location = $"/tmp/schema-cache-{Guid.NewGuid():N}.md"
              enabled = true }

        let passages =
            [ keywordPassage source 0 "The guide covers emergency dental scheduling."
              keywordPassage source 1 "The guide excludes cosmetic dental scheduling." ]

        let options =
            keywordOptions schemaVersion (new InvalidJsonSchemaChatClient() :> IChatClient)

        seedKeywordCache storageRoot source options passages.Head [ "cached scheduling term" ]
        let logs = ResizeArray<string>()

        let! enriched, _ = KnowledgeSources.attachKeywords storageRoot logs.Add options source passages

        Assert.Equal<string list>([ "cached scheduling term" ], enriched.Head.keywords)
        Assert.Empty(enriched.Tail.Head.keywords)

        let schemaRejectionLogs =
            logs
            |> Seq.filter (fun log -> log.Contains("OpenAI rejected keyword schema and response-format fallbacks"))
            |> Seq.length

        Assert.Equal(1, schemaRejectionLogs)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``tool loader includes current time tool from tools project`` () =
    let providerFolder =
        System.IO.Path.GetDirectoryName(typeof<FsVoice.Tools.CurrentTimeToolProvider>.Assembly.Location)

    let catalog = QaToolLoader.load (FakeQaToolHost()) (Some providerFolder)

    let hasCurrentTimeTool =
        catalog.tools
        |> List.exists (fun tool -> tool.PluginName = "FsVoiceTools" && tool.Name = "current_time")

    Assert.True(hasCurrentTimeTool)

[<Fact>]
let ``rank promotes exact section target over lexical distractors`` () =
    async {
        let source =
            { kind = Pdf
              location = "/tmp/paper.pdf"
              enabled = true }

        let retrieval =
            { KnowledgeSources.emptyIndex with
                sources = [ source ]
                chunks =
                    [ { source = source
                        index = 1
                        text = "Section: Notes\nThis appendix mentions abstract abstract abstract."
                        score = 0.0f }
                      { source = source
                        index = 2
                        text = "Section: ABSTRACT\nThis paper introduces a retrieval method."
                        score = 0.0f } ] }

        let! chunks = KnowledgeSources.rank None false false true ignore "Can you summarize the abstract?" 1 retrieval

        Assert.Equal(2, chunks.Head.index)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``rank promotes inline section label over checklist mentions`` () =
    async {
        let source =
            { kind = Pdf
              location = "/tmp/paper.pdf"
              enabled = true }

        let retrieval =
            { KnowledgeSources.emptyIndex with
                sources = [ source ]
                chunks =
                    [ { source = source
                        index = 1
                        text =
                          "The NeurIPS checklist asks whether claims made in the abstract and introduction reflect the paper. The paper abstract should summarize contributions."
                        score = 0.0f }
                      { source = source
                        index = 2
                        text =
                          "Abstract While LLM agents can use external tools, they require adaptive memory systems to leverage historical experiences."
                        score = 0.0f } ] }

        let! chunks = KnowledgeSources.rank None false false true ignore "summarize the paper abstract" 1 retrieval

        Assert.Equal(2, chunks.Head.index)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``rank corrects misspelled section target before searching`` () =
    async {
        let source =
            { kind = Pdf
              location = "/tmp/paper.pdf"
              enabled = true }

        let retrieval =
            { KnowledgeSources.emptyIndex with
                sources = [ source ]
                chunks =
                    [ { source = source
                        index = 1
                        text = "Section: Notes\nThis appendix mentions abstract abstract abstract."
                        score = 0.0f }
                      { source = source
                        index = 2
                        text = "Section: ABSTRACT\nThis paper introduces a retrieval method."
                        score = 0.0f } ] }

        let! chunks = KnowledgeSources.rank None false false true ignore "Can you summarize the abtract?" 1 retrieval

        Assert.Equal(2, chunks.Head.index)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``rank keeps coverage from each selected source for multi-document questions`` () =
    async {
        let sourceA =
            { kind = Pdf
              location = "/tmp/source-a.pdf"
              enabled = true }

        let sourceB =
            { kind = Pdf
              location = "/tmp/source-b.pdf"
              enabled = true }

        let retrieval =
            { KnowledgeSources.emptyIndex with
                sources = [ sourceA; sourceB ]
                chunks =
                    [ { source = sourceA
                        index = 1
                        text = "Latency latency latency for source A."
                        score = 0.0f }
                      { source = sourceA
                        index = 2
                        text = "Latency latency latency architecture in source A."
                        score = 0.0f }
                      { source = sourceB
                        index = 1
                        text = "Latency tradeoffs for source B."
                        score = 0.0f } ] }

        let! chunks = KnowledgeSources.rank None false false true ignore "Compare both documents on latency" 2 retrieval

        let sourceLocations =
            chunks |> List.map (fun chunk -> chunk.source.location) |> Set.ofList

        Assert.Equal<int>(2, chunks.Length)
        Assert.Contains(sourceA.location, sourceLocations)
        Assert.Contains(sourceB.location, sourceLocations)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``rank returns representative content for broad selected-document summaries`` () =
    async {
        let sourceA =
            { kind = Pdf
              location = "/tmp/broad-a.pdf"
              enabled = true }

        let sourceB =
            { kind = Pdf
              location = "/tmp/broad-b.pdf"
              enabled = true }

        let retrieval =
            { KnowledgeSources.emptyIndex with
                sources = [ sourceA; sourceB ]
                chunks =
                    [ { source = sourceA
                        index = 1
                        text = "Quantum scheduling methods are introduced here."
                        score = 0.0f }
                      { source = sourceB
                        index = 1
                        text = "Energy market simulations are introduced here."
                        score = 0.0f } ] }

        let! chunks = KnowledgeSources.rank None false false true ignore "Summarize these documents" 4 retrieval

        let sourceLocations =
            chunks |> List.map (fun chunk -> chunk.source.location) |> Set.ofList

        Assert.Contains(sourceA.location, sourceLocations)
        Assert.Contains(sourceB.location, sourceLocations)
    }
    |> Async.RunSynchronously

[<Fact>]
let ``durable memory types explicit user preference as directive`` () =
    let snapshot =
        { turnId = "turn_preference"
          itemId = "item_preference"
          revision = 1
          text = "Remember that I prefer concise technical answers."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow }

    let proposals =
        DurableMemory.proposalsFromExchange snapshot "Okay, I will remember that."

    let _ = Assert.Single proposals

    Assert.Equal(Directive, proposals.Head.kind)
    Assert.Contains("concise", proposals.Head.text, StringComparison.OrdinalIgnoreCase)

[<Fact>]
let ``durable memory hot overlay makes committed memory immediately recallable`` () =
    let snapshot =
        { turnId = "turn_decision"
          itemId = "item_decision"
          revision = 1
          text = "We decided to use typed memory records for realtime recall."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow }

    let proposals =
        DurableMemory.proposalsFromExchange snapshot "That decision is recorded."

    let store, updates, _ =
        DurableMemory.commitProposals (DurableMemory.inMemory []) proposals

    let spec =
        { query = "typed memory realtime recall decision"
          kinds = [ Decision ]
          scopes = [ Session ]
          namespaceId = DurableMemory.defaultNamespace
          temporalMode = CurrentOnly
          temporalReference = None
          includeSuperseded = false
          recallBudget = Fast
          maxCandidates = 10
          minScore = None
          latencyBudget = TimeSpan.FromMilliseconds 90. }

    let hits = DurableMemory.recall None spec store |> Async.RunSynchronously

    let _ = Assert.Single updates

    Assert.NotEmpty hits
    Assert.Equal(Decision, hits.Head.record.kind)

[<Fact>]
let ``durable memory correction supersedes related directive`` () =
    let original =
        { turnId = "turn_original"
          itemId = "item_original"
          revision = 1
          text = "I prefer verbose technical answers."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow.AddMinutes(-1.) }

    let correction =
        { turnId = "turn_correction"
          itemId = "item_correction"
          revision = 2
          text = "Actually, I prefer concise technical answers."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow }

    let store, _, _ =
        DurableMemory.commitProposals
            (DurableMemory.inMemory [])
            (DurableMemory.proposalsFromExchange original "Noted.")

    let store, updates, _ =
        DurableMemory.commitProposals store (DurableMemory.proposalsFromExchange correction "Updated.")

    let currentDirectives =
        store.records
        |> List.filter (fun record -> record.kind = Directive && record.status = Current)

    let supersededDirectives =
        store.records
        |> List.filter (fun record -> record.kind = Directive && record.status = Superseded)

    Assert.True(updates |> List.exists (fun update -> update.outcome = Contradiction))
    let _ = Assert.Single currentDirectives
    let _ = Assert.Single supersededDirectives

    Assert.Contains("concise", currentDirectives.Head.text, StringComparison.OrdinalIgnoreCase)

[<Fact>]
let ``durable memory forget request retracts related current record`` () =
    let original =
        { turnId = "turn_original_forget"
          itemId = "item_original_forget"
          revision = 1
          text = "I prefer verbose implementation notes."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow.AddMinutes(-1.) }

    let forget =
        { turnId = "turn_forget"
          itemId = "item_forget"
          revision = 2
          text = "Forget my preference for verbose implementation notes."
          isFinal = true
          receivedAt = DateTimeOffset.UtcNow }

    let store, _, _ =
        DurableMemory.commitProposals
            (DurableMemory.inMemory [])
            (DurableMemory.proposalsFromExchange original "Noted.")

    let store, logs = DurableMemory.retractFromTurn store forget

    Assert.True(
        logs
        |> List.exists (fun log -> log.Contains("Retracted", StringComparison.OrdinalIgnoreCase))
    )

    Assert.True(
        store.records
        |> List.exists (fun record -> record.kind = Directive && record.status = Retracted)
    )
